module Sum.
import Top.
import Bool.
import Cast.

inj1u ◂ Top = β{λ a. λ i1. λ i2. i1 a}.
inj2u ◂ Top = β{λ b. λ i1. λ i2. i2 b}.

Sum' ◂ ★ ➔ ★ ➔ Top ➔ ★ = λ A : ★. λ B : ★. λ x : Top.
  ∀ X : Top ➔ ★. (Π a : A. X β{inj1u a}) ➔ (Π b : B. X β{inj2u b}) ➔ X x.
Sum ◂ ★ ➔ ★ ➔ ★ = λ A : ★. λ B : ★. ι x : Top. Sum' · A · B x.

inj1 ◂ ∀ A : ★. A ➔ ∀ B : ★. Sum · A · B =
  Λ A. λ a. Λ B. [β{inj1u a}, Λ X. λ i1. λ i2. i1 a].

inj2 ◂ ∀ B : ★. B ➔ ∀ A : ★. Sum · A · B =
  Λ B. λ b. Λ A. [β{inj2u b}, Λ X. λ i1. λ i2. i2 b].

SumInd ◂ ∀ A : ★. ∀ B : ★. Π s : Sum · A · B. ∀ Q : Sum · A · B ➔ ★.
         (Π a : A. Q (inj1 a · B)) ➔ (Π b : B. Q (inj2 b · A)) ➔ Q s =
  Λ A. Λ B. λ s. Λ Q. λ i1. λ i2. s.2 · (λ x : Top. ∀ x' : Sum · A · B. {x' ≃ x} ➾ Q x')
    (λ a. Λ x. Λ e. ρ e - (i1 a))
    (λ b. Λ x. Λ e. ρ e - (i2 b))
    -s -β.


SumFold ◂ ∀ A : ★. ∀ B : ★. Sum · A · B ➔ ∀ X : ★. (A ➔ X) ➔ (B ➔ X) ➔ X =
  Λ A. Λ B. λ s. Λ X. s.2 · (λ x : Top. X).

SumMap ◂ ∀ A : ★. ∀ B : ★. Sum · A · B ➔ ∀ C : ★. ∀ D : ★. (A ➔ C) ➔ (B ➔ D) ➔ Sum · C · D =
  Λ A. Λ B. λ s. Λ C. Λ D. λ f1. λ f2. φ (θ<s> (SumInd s) (λ a. β) (λ b. β)) -
    (SumFold s · (Sum · C · D) (λ a. inj1 (f1 a) · D) (λ b. inj2 (f2 b) · C))
    {λ i1. λ i2. s (λ a. i1 (f1 a)) (λ b. i2 (f2 b))}.

cast-inj1 ◂ ∀ A : ★. ∀ B : ★. Sum · A · B ➔ ∀ C : ★. Cast · A · C ➾ Sum · C · B =
  Λ A. Λ B. λ s. Λ C. Λ c. φ (θ<s> (SumInd s) (λ a. β) (λ b. β)) -
    (SumFold s · (Sum · C · B) (λ a. inj1 (cast -c a) · B) (λ b. inj2 b · C)) {s}.

cast-inj2 ◂ ∀ A : ★. ∀ B : ★. Sum · A · B ➔ ∀ C : ★. Cast · B · C ➾ Sum · A · C =
  Λ A. Λ B. λ s. Λ C. Λ c. φ (θ<s> (SumInd s) (λ a. β) (λ b. β)) -
    (SumFold s · (Sum · A · C) (λ a. inj1 a · C) (λ b. inj2 (cast -c b) · A)) {s}.

sum-1-2 ◂ ∀ A : ★. ∀ B : ★. Sum · A · B ➔ Bool =
  Λ A. Λ B. λ s. SumFold s · Bool (λ a. tt) (λ b. ff).

SumIsInd ◂ ∀ A : ★. ∀ B : ★. Π s : Sum · A · B. ∀ X : ★.
         (Π a : A. {sum-1-2 s ≃ tt} ➾ X) ➔ (Π b : B. {sum-1-2 s ≃ ff} ➾ X) ➔ X =
  Λ A. Λ B. λ s. Λ X. φ (θ<s> (SumInd s) (λ a. β) (λ b. β)) -
    (θ<s> (SumInd · A · B s) (λ a. λ i1. λ i2. i1 a -β) (λ b. λ i1. λ i2. i2 b -β)) {s}.

extract-inj1 ◂ ∀ A : ★. ∀ B : ★. Π s : Sum · A · B. {sum-1-2 s ≃ tt} ➾ A =
  Λ A. Λ B. λ s. θ<s> (SumInd s) (λ a. Λ e. a) (λ b. tt-ff-raa'' · A).

extract-inj2 ◂ ∀ A : ★. ∀ B : ★. Π s : Sum · A · B. {sum-1-2 s ≃ ff} ➾ B =
  Λ A. Λ B. λ s. θ<s> (SumInd s) (λ a. tt-ff-raa' · B) (λ b. Λ e. b).
