module Cast.

Cast ◂ ★ ➔ ★ ➔ ★ = λ A : ★. λ B : ★.
  ι cast : A ➔ B. {cast ≃ λ x. x}.

CastD ◂ Π A : ★. Π B : A ➔ ★. ★ = λ A : ★. λ B : A ➔ ★.
  ι cast : Π a : A. B a. {cast ≃ λ x. x}.

CastI ◂ Π I : ★. (I ➔ ★) ➔ (I ➔ ★) ➔ ★ = λ I : ★. λ A : I ➔ ★. λ B : I ➔ ★.
  ι cast : ∀ i : I. A i ➔ B i. {cast ≃ λ x. x}.

CastP ◂ (★ ➔ ★) ➔ (★ ➔ ★) ➔ ★ = λ A : ★ ➔ ★. λ B : ★ ➔ ★.
  ι cast : ∀ P : ★. A · P ➔ B · P. {cast ≃ λ x. x}.

CastΠ ◂ Π I : ★. (★ ➔ I ➔ ★) ➔ (★ ➔ I ➔ ★) ➔ ★ = λ I : ★. λ A : ★ ➔ I ➔ ★. λ B : ★ ➔ I ➔ ★.
  ι cast : ∀ P : ★. ∀ i : I. A · P i ➔ B · P i. {cast ≃ λ x. x}.

Cast2 ◂ ★ ➔ ★ ➔ ★ = λ A : ★. λ B : ★.
  ι c : A ➔ B.
  ι _ : B ➔ A.
  ι _ : ∀ a : A. {c a ≃ a}.
        ∀ b : B. {c b ≃ b}.

mk-cast ◂ ∀ A : ★. ∀ B : ★. ∀ f : A ➔ B. {f ≃ λ x. x} ➾ Cast · A · B =
  Λ A. Λ B. Λ f. Λ e. [φ e - f {β}, β].

castId ◂ ∀ A : ★. Cast · A · A = Λ A. [λ a. a, β].

cast1 ◂ ∀ A : ★. ∀ B : ★. Cast2 · A · B ➾ A ➔ B =
  Λ A. Λ B. Λ c. λ a. φ (c.2.2.1 -a) - (c.1 a) {a}.

cast2 ◂ ∀ A : ★. ∀ B : ★. Cast2 · A · B ➾ B ➔ A =
  Λ A. Λ B. Λ c. λ b. φ (c.2.2.2 -b) - (c.2.1 b) {b}.

cast ◂ ∀ A : ★. ∀ B : ★. Cast · A · B ➾ A ➔ B =
  Λ A. Λ B. Λ c. λ a. φ (ρ c.2 - β) - (c.1 a) {a}.

castE ◂ ∀ A : ★. ∀ B : ★. Cast · A · B ➔ A ➔ B =
  Λ A. Λ B. λ c. cast · A · B -c.

castD ◂ ∀ A : ★. ∀ B : A ➔ ★. CastD · A · B ➾ Π a : A. B a =
  Λ A. Λ B. Λ c. λ a. φ (ρ c.2 - β) - (c.1 a) {a}.

castI ◂ ∀ I : ★. ∀ A : I ➔ ★. ∀ B : I ➔ ★. CastI · I · A · B ➾ ∀ i : I. A i ➔ B i =
  Λ I. Λ A. Λ B. Λ c. Λ i. λ a. φ (ρ c.2 - β) - (c.1 -i a) {a}.

castP ◂ ∀ A : ★ ➔ ★. ∀ B : ★ ➔ ★. ∀ P : ★. CastP · A · B ➾ A · P ➔ B · P =
  Λ A. Λ B. Λ P. Λ c. λ a. φ (ρ c.2 - β) - (c.1 a) {a}.

castΠ ◂ ∀ I : ★. ∀ A : ★ ➔ I ➔ ★. ∀ B : ★ ➔ I ➔ ★. CastΠ · I · A · B ➾ ∀ P : ★. ∀ i : I. A · P i ➔ B · P i =
  Λ I. Λ A. Λ B. Λ c. Λ P. Λ i. λ a. φ (ρ c.2 - β) - (c.1 -i a) {a}.
