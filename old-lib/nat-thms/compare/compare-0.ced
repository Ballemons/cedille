import nat.
import not.

compare-eq ◂ Π x : Nat . Π y : Nat . (compare x y ≃ EQ) ➔ x ≃ y =
  λ x . θ x
    (λ p . λ r . λ y . θ y
      (λ p' . λ r' . λ u . ρ (r p' u) - β)
      (λ u . δ u) )
    (λ y . θ y
      (λ p' . λ r' . λ u . δ u)
      (λ u . β) ) .

lt-compare ◂ Π x : Nat . Π y : Nat. Lt x y ➔ compare x y ≃ LT =
  λ x . λ y . λ u . θ+ (compare x y) (λ v . v) (λ v . δ (ρ+ v - u)) (λ v . δ (ρ+ v - u)) .

eqnat-eq ◂ Π x : Nat . Π y : Nat . (eqnat x y ≃ tt) ➔ x ≃ y = 
  λ x . λ y . λ u . θ+ (compare x y)
    (λ v . δ (ρ v - εl u) )
    (compare-eq x y)
    (λ v . δ (ρ v - εl u) ) .

eqnat-S ◂ Π x : Nat . eqnat x (S x) ≃ ff =
  λ x . θ x (λ p . λ u . ρ u - β) β .

eqnat-refl ◂ Π x : Nat . eqnat x x ≃ tt =
  λ x . θ x ( λ px . λ h . ρ h - β ) β .

eqnat-sym ◂ Π x : Nat . Π y : Nat . eqnat x y ≃ eqnat y x = 
  λ x . θ x 
    (λ x . λ u . λ y . θ y
      ( λ y . λ v . (u y) )
      β )
    (λ y . θ y
      ( λ py . λ u . β )
      β ) .

eqnat-defn ◂ Π x : Nat . Π y : Nat . ( x ≃ y ) ➔ eqnat x y ≃ tt =
  λ x . λ y . λ pf . ρ pf - ρ ( eqnat-refl y ) - β .

eqnat-not-eq ◂ Π x : Nat . Π y : Nat. (eqnat x y ≃ ff) ➔ Not · (x ≃ y) =
  λ x . λ y . λ u . λ v . δ (ρ (ρ v - u) - (eqnat-refl y)).

Lte-Z ◂ Π x : Nat . Lte x Z ➔ x ≃ Z =
  λ x . θ x (λ n . λ r . λ u . δ u) (λ u . β) .
  
Z-Lte ◂ Π x : Nat . Lte Z x =
  λ x . θ x (λ n . λ _ . β) β .

Z-Lt ◂ Π x : Nat . Lt Z (S x) =
  λ x . β .

Not-Lt-Z ◂ Π x : Nat . Not · (Lt x Z) =
  λ x . θ x (λ n . λ r . λ u . δ u) (λ u . δ u) .

Gte-Z ◂ Π x : Nat . Gte x Z =
  λ x . θ x (λ n . λ _ . β) β .

Lte-refl ◂ Π x : Nat . Lte x x =
  λ x . θ x (λ p . λ r . r) β .

Lt-S ◂ Π x : Nat . Lt x (S x) =
  λ x . θ x (λ x . λ ih . ih) β .

Lt-Lte ◂ Π x : Nat . Π y : Nat. Lt x y ➔ Lte x y =
  λ x . λ y . λ v . θ+ (compare x y) (λ u . ρ+ u - β) (λ u . δ (ρ+ u - v)) (λ u . δ (ρ+ u - v)) .

Lte-S-Lt ◂ Π x : Nat . Π y : Nat . Lte (S x) y ➔ Lt x y =
  λ x . θ x (λ px . λ r . λ y .
               θ y (λ py . λ _ . λ u . r py u) (λ u . δ u))
            (λ y . 
               θ y (λ py . λ _ . λ u . β) (λ u . δ u)).

Lte-S-0 ◂ Π x : Nat . Π y : Nat . Lte (S x) y ➔ Lte x y = 
 λ x . λ y . λ u . Lt-Lte x y (Lte-S-Lt x y u).

compare-eq-trans ◂ Π x : Nat . Π y : Nat . Π z : Nat . Π o : compare-t . ( compare x y ≃ EQ ) ➔ ( compare y z ≃ o ) ➔ ( compare x z ≃ o ) =
  λ x . λ y . λ z . λ o . λ pf-xy . λ pf-yz . ρ ( compare-eq x y pf-xy ) - ρ ( ς pf-yz ) - β .

compare-trans-eq ◂ Π x : Nat . Π y : Nat . Π z : Nat . Π o : compare-t . ( compare x y ≃ o ) ➔ ( compare y z ≃ EQ ) ➔ ( compare x z ≃ o ) =
  λ x . λ y . λ z . λ o . λ pf-xy . λ pf-yz . ρ ( ς pf-xy ) - ρ ( compare-eq y z pf-yz ) - β .

Lt-not-eqnat ◂ Π x : Nat . Π y : Nat . Lt x (S y) ➔ (eqnat x y ≃ ff) ➔ Lt x y =
 λ x . θ x (λ px . λ ih . λ y . θ y (λ py . λ _ . λ u . λ v . ih py u v) (λ u . λ v . δ (Not-Lt-Z px u · (ff ≃ tt))))
           (λ y . θ y (λ py . λ _ . λ u . λ v . β) (λ u . λ v . δ v)).

Lte-not-eqnat ◂ Π x : Nat . Π y : Nat . Lte x y ➔ Not · (x ≃ y) ➔ Lt x y =
  λ x . λ y . θ+ (compare x y)
                 (λ a . λ u . λ v . ρ+ a - β)
                 (λ a . λ u . λ v . δ (v (eqnat-eq x y (ρ+ a - β)) · (ff ≃ tt)))
                 (λ a . λ u . λ v . δ (ρ+ a - u)) .

import nat-thms/compare/compare-trans.

Lt-trans ◂ Π x : Nat . Π y : Nat . Π z : Nat. Lt x y ➔ Lt y z ➔ Lt x z =
  λ x . λ y . λ z . λ u . λ v . ρ+ (compare-trans x y z LT (lt-compare x y u) (lt-compare y z v)) - β.

Lt-S-Lte ◂ Π x : Nat. Π y : Nat. Lt x (S y) ➔ Lte x y =
  λ x . θ x (λ px . λ ih . λ y . 
               θ y (λ y . λ _ . λ u . ih y u) (λ u . δ (Not-Lt-Z px u · (ff ≃ tt))))
            (λ y . λ u . Z-Lte y) .

lte-SS ◂ Π x : Nat . Π y : Nat . lte (S x) (S y) ≃ lte x y = λ x . λ y . β .
gt-SS ◂ Π x : Nat . Π y : Nat . gt (S x) (S y) ≃ gt x y = λ x . λ y . β .

compare-lt ◂ Π x : Nat . Π y : Nat . (compare x y ≃ LT) ➔ Lt x y =
  λ x . θ x
    ( λ px . λ hx . λ y . θ y
      (λ py . λ _ . λ h . (hx py h) )
      (λ h . δ h) )
    ( λ y . θ y
      ( λ _ . λ _ . λ _ . β )
      ( λ h . δ h ) ) .

compare-gt ◂ Π x : Nat . Π y : Nat . (compare x y ≃ GT) ➔ Gt x y =
  λ x . θ x
    ( λ px . λ hx . λ y . θ y
      (λ py . λ _ . λ h . (hx py h) )
      (λ h . β ) )
    ( λ y . θ y
      ( λ py . λ hy . λ h . δ h )
      ( λ h . δ h ) ) .

lte-not-gt ◂ Π x : Nat . Π y : Nat . (lte x y ≃ gt x y) ➔ False =
λ x . θ x
    ( λ px . λ hx . λ y . θ y
      ( λ py . λ hy . λ h . (hx py (ρ (lte-SS px py) - ρ (gt-SS px py) - h)) )
      (λ h . δ h ) )
    ( λ y . θ y
      (λ py . λ hy . λ h . δ h )
      ( λ h . δ h) ) .

Lte-trans ◂ Π x : Nat . Π y : Nat . Π z : Nat . Lte x y ➔ Lte y z ➔ Lte x z =
  λ x . λ y . λ z . λ hxy . λ hyz .
    θ+ (compare x y)
      ( λ xyLT .
        θ+ (compare y z)
	  ( λ yzLT . Lt-Lte x z (compare-lt x z (compare-trans x y z LT xyLT yzLT)) ) 
	  ( λ yzEQ . ρ (ς (compare-eq y z yzEQ)) - hxy )
	  (( λ yzGT . lte-not-gt y z (ρ (compare-gt y z yzGT) - hyz) · (Lte x z))))
      ( λ xyEQ . ρ (compare-eq x y xyEQ) - hyz )
      ( λ xyGT . lte-not-gt x y (ρ (compare-gt x y xyGT) - hxy) · (Lte x z) ) .

Lt-Lte-trans ◂ Π x : Nat . Π y : Nat . Π z : Nat . Lt x y ➔ Lte y z ➔ Lt x z =
  λ x . λ y . λ z . λ hxy . λ hyz .
    θ+ (compare y z)
       ( λyzLT . Lt-trans x y z hxy (compare-lt y z yzLT) )
       ( λyzEQ . ρ (ς (compare-eq y z yzEQ)) - hxy )
       ( λyzGT . lte-not-gt y z (ρ hyz - (ς (compare-gt y z yzGT))) · (Lt x z) ) .

Lte-Lt-trans ◂ Π x : Nat . Π y : Nat . Π z : Nat . Lte x y ➔ Lt y z ➔ Lt x z =
  λ x . λ y . λ z . λ hxy . λ hyz .
    θ+ (compare x y)
       ( λxyLT . Lt-trans x y z (compare-lt x y xyLT) hyz )
       ( λxyEQ . ρ (compare-eq x y xyEQ) - hyz )
       ( λxyGT . lte-not-gt x y ( ρ hxy - (ς (compare-gt x y xyGT))) · (Lt x z) ) .
