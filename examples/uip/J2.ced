module Uip.J2.
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% Het J implies Het K, which implies Het UIP

J2 ◂ ∀ A : ★ . ∀ a : A . ∀ P : (Π a' : A . (a ≃ a') ➔ ★) .
  Π p : P a β .
  ∀ a' : A . Π q : (a ≃ a') . P a' q
  = Λ A . Λ a . Λ P . λ p . Λ a' . λ q .
  ● .

K2 ◂ ∀ A : ★ . ∀ a : A . ∀ P : (a ≃ a) ➔ ★ .
  Π p : P β .
  Π q : (a ≃ a) . P q
  = Λ A . Λ a . Λ P .  λ p . λ q .
  J2 · A -a · (λ a' : A  . λ q' : (a ≃ a') . P β{q'})
  p -a β{q} .

UIP2 ◂ ∀ A : ★ . ∀ a : A .
  Π q1 : a ≃ a . Π q2 : a ≃ a .
  q1 ≃ q2
  = Λ A . Λ a . λ q1 . λ q2 .
  K2 · A -a · (λ x : (a ≃ a) . (x ≃ q2))
  (K2 · A -a · (λ x : (a ≃ a) . (β ≃ x))
  β q2) q1 .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% Het UIP + Kleene β is inconsistent

Id ◂ ★ = ∀ X : ★ . X ➔ X .
id ◂ Id = Λ X . λ x . x .

Bool ◂ ★ = ∀ X : ★ . X ➔ X ➔ X .
true ◂ Bool = Λ X . λ tt . λ ff . tt .
false ◂ Bool = Λ X . λ tt . λ ff . ff .

bottom ◂ true ≃ false =
  UIP2 · Id -id β{true} β{false} .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
