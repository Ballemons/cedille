%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

BoolC ◂ ★ = ∀ X : ★ . X ➔ X ➔ X.

trueC ◂ BoolC = Λ X . λ cT . λ cF . cT .
falseC ◂ BoolC = Λ X . λ cT . λ cF . cF .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

BoolP ◂ BoolC ➔ ★ = λ bC : BoolC .
  ∀ X : ★ . ∀ P : X ➔ ★ .
  ∀ cT : X . ∀ cF : X .
  P cT ➔
  P cF ➔
  P (bC · X cT cF) .

trueP ◂ BoolP trueC =
  Λ X . Λ P . Λ cT . Λ cF . λ pT . λ pF .
  pT .

falseP ◂ BoolP falseC =
  Λ X . Λ P . Λ cT . Λ cF . λ pT . λ pF .
  pF .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

BoolR ◂ BoolC ➔ ★ = λ bC : BoolC .
  bC · BoolC trueC falseC ≃ bC .

trueR ◂ BoolR trueC = β .
falseR ◂ BoolR falseC = β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Bool ◂ ★ = ι b : (ι bC : BoolC . BoolP bC) . BoolR b.1 .

mkBool ◂ Π b : (ι bC : BoolC . BoolP bC) . BoolR b.1 ➾ Bool =
  λ b . Λ q . [ b , ρ q - β{b} ] .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

true ◂ Bool = mkBool [ trueC , trueP ] -β .
false ◂ Bool = mkBool [ falseC , falseP ] -β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

elimBool' ◂ Π b : Bool .
  ∀ P : Bool ➔ ★ .
  P true ➔
  P false ➔
  P b
  = λ b . Λ P .
  ρ ς b.2 -
  (b.1.2 · Bool · P -true -false) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

elimBool ◂ ∀ P : Bool ➔ ★ .
  P true ➔
  P false ➔
  Π b : Bool . P b
  = Λ P . λ pT . λ pF . λ b .
  elimBool' b · P pT pF .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
