module ListVecReuse.

import Sigma.
import Unit.
import Nat.
import Id.
import IIdMapping.
import IFixIndM.
import ReuseForgetful.
import ReuseEnriching.
import ListF.
import List.
import VecF.
import Vec.


-- cast from signature of vectors (VecF) to signature of lists (ListF)
vf2lf ◂ ∀ A : ★. ∀ X : Nat ➔ ★. ∀ Y :  ★. 
 (∀ n : Nat. IdExt · (X n) · Y) ➾
 ∀ n : Nat. IdExt · (VecF · A · X n) · (ListF · A · Y) 
 = Λ A. Λ X. Λ Y. Λ c. Λ n. λ v. pair · (ListF · A · Y) · (λ b : ListF · A · Y . {b ≃ v}) (v.1 · (ListF · A · Y ) 
 (Λ p. in1lf · A · Y) (Λ m. Λ p. λ a. λ x. in2lf · A · Y a (elimIdExt · (X m) · (Y ) -(c -m) x))) (rebVec · A · X -n v).


-- "forgetful" cast from vectors to lists in terms of generic
-- combinator iF2iG
v2l ◂ ∀ A : ★. ∀ n : Nat. IdExt · (Vec · A n) · (List · A) = Λ A. Λ n. forget · Nat · (VecF · A) -(vfimap · A)
  · (ListF · A) -(lfimap · A)  -(Λ X. Λ Y. λ c. Λ i. vf2lf · A · X · Y -c -i) -n. 


-- cast from signature of lists (ListF) to signature of vectors (VecF)
lf2vf ◂ ∀ A : ★. ∀ X : Nat ➔ ★. ∀ Y : ★. 
 Π f : Y ➔ Nat. 
 (IdExtDep · Y · (λ y : Y . X (f y))) ➔ 
   IdExtDep · (ListF · A · Y ) · (λ gy : ListF · A · Y . VecF · A · X (lenAlgM · A · Y f  gy))
 = Λ A. Λ X. Λ Y. λ f.  λ c. λ l. pair · (VecF · A · X (lenAlgM · A · Y f  l)) · (λ b : VecF · A · X (lenAlgM · A · Y f  l) . {b ≃ l}) 
  (ListFInd · A · Y l ·  (λ l : (ListF · A · Y). VecF · A · X (lenAlgM · A · Y f  l)) 
  (λ a. λ y. in2vf · A · X -(f  y) a (elimIdExtDep · (Y) · (λ y : Y . X (f  y)) -c y)) (in1vf · A · X))  
  (rebList · A · Y l).
 

-- "enriching" cast from list to vector in terms of generic combinator iG2iF
l2v ◂ ∀ A : ★. IdExtDep · (List · A) · (λ l : List · A. Vec · A (length · A l)) = Λ A. enrich · Nat · (VecF · A) -(vfimap · A)
 ·  (ListF · A) -(lfimap · A) (lenAlgM · A)  -(Λ X. Λ Y. λ f. λ a.  lf2vf · A · X · Y f a) .
