import Nat.
import List.
import Vec.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

v2lC ◂ ∀ A : ★ . ∀ n : Nat . VecC · A n ➔ ListC · A
  = Λ A . Λ n . λ xsC .
  Λ X . λ cN . λ cC .
  xsC · (λ _ : Nat . X) cN (Λ _ . cC) .

v2lP ◂ ∀ A : ★ . ∀ n : Nat . ∀ xsC : VecC · A n .
  VecP · A n xsC ➔ ListP · A (v2lC · A -n xsC)
  = Λ A . Λ n . Λ _ . λ xsP .
  Λ X . Λ P . Λ cN . Λ cC . λ pN . λ pC .
  xsP · (λ _ : Nat . X) · (λ _ : Nat . P)
    -cN -(Λ _ . cC)
    pN (Λ _ . pC) .

v2l ◂ ∀ A : ★ . ∀ n : Nat . Vec · A n ➔ List · A
  = Λ A . Λ n . λ xs . mkList · A
  [ v2lC · A -n xs.1.1
  , v2lP · A -n -xs.1.1 xs.1.2 ]
  xs.2 .

v2lId ◂ v2l ≃ λ x . x = β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

l2vC ◂ ∀ A : ★ . Π xs : List · A . VecC · A (length · A xs)
  = Λ A . λ xs . Λ X .
  λ cN . λ cC . elimList · A ·
  (λ xs' : List · A . X (length · A xs'))
  cN
  (Λ xs' . cC -(length · A xs'))
  xs .

l2vP ◂ ∀ A : ★ . Π xs : List · A . VecP · A (length · A xs) (l2vC · A xs)
  = Λ A . λ xs .
  Λ X . Λ P . Λ cN . Λ cC . λ pN . λ pC . elimList · A ·
  (λ xs' : List · A . P (length · A xs') (l2vC · A xs' · X cN cC))
  pN
  (Λ xs' . pC -(length · A xs') -(l2vC · A xs' · X cN cC))
  xs .

l2v ◂ ∀ A : ★ . Π xs : List · A . Vec · A (length · A xs)
  = Λ A . λ xs . mkVec · A -(length · A xs)
  [ l2vC · A xs , l2vP · A xs ]
  xs.2 .

l2vId ◂ l2v ≃ λ x . x = β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

imapCL ◂ ∀ A : ★ . ∀ B : ★ .  Π f : A ➔ B . ListC · A ➔ ListC · B
  = Λ A . Λ B . λ f . λ xsC .
  Λ X . λ cN . λ cC . xsC · X cN (λ x . cC (f x)) .

imapPL ◂ ∀ A : ★ . ∀ B : ★ .
  Π f : A ➔ B . (f ≃ λ x . x) ➔
  ∀ xsC : ListC · A .
  ListP · A xsC ➔ ListP · B (imapCL · A · B f xsC)
  = Λ A . Λ B . λ f . λ q . Λ _ . λ xsP .
  Λ X . Λ P . Λ cN . Λ cC . λ pN . λ pC .
  xsP · X · P
    -cN -(λ x . cC (f x))
    pN (Λ xsC . λ x . pC -xsC (f x)) .

imapL ◂ ∀ A : ★ . ∀ B : ★ .  Π f : A ➔ B . (f ≃ λ x . x) ➾ List · A ➔ List · B
  = Λ A . Λ B . λ f . Λ q . λ xs . mkList · B
  [ imapCL · A · B f xs.1.1
  , imapPL · A · B f q -xs.1.1 xs.1.2 ]
  ρ q - xs.2 .

imapIdL ◂ imapL (λ x . x) ≃ λ x . x = β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

vs2lsL ◂ ∀ A : ★ . ∀ n : Nat .
  List · (Vec · A n) ➔ List · (List · A)
  = Λ A . Λ n . imapL · (Vec · A n) · (List · A) (v2l · A -n) -β .

vs2lsIdL ◂ vs2lsL ≃ λ x . x = β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
