import Nat.
import List.
import Vec.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

v2lC ◂ ∀ A : ★ . ∀ n : Nat . VecC · A n ➔ ListC · A
  = Λ A . Λ n . λ xsC .
  Λ X . λ cN . λ cC .
  xsC · (λ _ : Nat . X) cN (Λ _ . cC) .

v2lP ◂ ∀ A : ★ . ∀ n : Nat . ∀ xsC : VecC · A n .
  VecP · A n xsC ➔ ListP · A (v2lC · A -n xsC)
  = Λ A . Λ n . Λ _ . λ xsP .
  Λ X . Λ P . Λ cN . Λ cC . λ pN . λ pC .
  xsP · (λ _ : Nat . X) · (λ _ : Nat . P)
    -cN -(Λ _ . cC)
    pN (Λ _ . pC) .

v2l ◂ ∀ A : ★ . ∀ n : Nat . Vec · A n ➔ List · A
  = Λ A . Λ n . λ xs . mkList · A
  [ v2lC · A -n xs.1.1
  , v2lP · A -n -xs.1.1 xs.1.2 ]
  xs.2 .

v2lId ◂ ∀ A : ★ . ∀ n : Nat .
  v2l · A -n ≃ λ x . x
  = Λ A . Λ n . β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

imapCL ◂ ∀ A : ★ . ∀ B : ★ .  Π f : A ➔ B . ListC · A ➔ ListC · B
  = Λ A . Λ B . λ f . λ xsC .
  Λ X . λ cN . λ cC . xsC · X cN (λ x . cC (f x)) .

imapPL ◂ ∀ A : ★ . ∀ B : ★ .
  Π f : A ➔ B . (f ≃ λ x . x) ➔
  ∀ xsC : ListC · A .
  ListP · A xsC ➔ ListP · B (imapCL · A · B f xsC)
  = Λ A . Λ B . λ f . λ q . Λ _ . λ xsP .
  Λ X . Λ P . Λ cN . Λ cC . λ pN . λ pC .
  xsP · X · P
    -cN -(λ x . cC (f x))
    pN (Λ xsC . λ x . pC -xsC (f x)) .

imapL ◂ ∀ A : ★ . ∀ B : ★ .  Π f : A ➔ B . (f ≃ λ x . x) ➔ List · A ➔ List · B
  = Λ A . Λ B . λ f . λ q . λ xs . mkList · B
  [ imapCL · A · B f xs.1.1
  , imapPL · A · B f q -xs.1.1 xs.1.2 ]
  ρ q - (ρ xs.2 - β) .

imapIdL ◂ ∀ A : ★ .
  imapL · A · A (λ x . x) β ≃ λ x . x
  = Λ A . β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

vs2lsL ◂ ∀ A : ★ . ∀ n : Nat .
  List · (Vec · A n) ➔ List · (List · A)
  = Λ A . Λ n . imapL · (Vec · A n) · (List · A) (v2l · A -n) β .

vs2lsIdL ◂ ∀ A : ★ . ∀ n : Nat .
  vs2lsL · A -n ≃ λ x . x
  = Λ A . Λ n . β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
