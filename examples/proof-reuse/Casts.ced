import Nat.
import List.
import Vec.
import Cast.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

cmapCastL ◂ ∀ A : ★ . ∀ B : ★ .
  Cast' · A · B ➾ Cast' · (List · A) · (List · B)
  = Λ A . Λ B . Λ c . elimList · A ·
  (λ xs : List · A . ι ys : List · B . ys ≃ xs)
  [ nilL · B , β{nilL · B} ]
  (Λ xs . λ x . λ ih .
    [ consL · B (cast' · A · B -c x) ih.1 ,
      ρ ih.2 - β{consL · B (cast' · A · B -c x) ih.1} ]) .

cmapL ◂ ∀ A : ★ . ∀ B : ★ . Cast' · A · B ➾ List · A ➔ List · B
  = Λ A . Λ B . Λ c .
    cast' · (List · A) · (List · B)
      -(cmapCastL · A · B -c) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

vs2usL ◂ ∀ A : ★ . ∀ n : Nat .
  List · (Vec · A n) ➔ List · (VecL · A n)
  = Λ A . Λ n . cmapL · (Vec · A n) · (VecL · A n)
    -(v2uCast · A -n) .

us2lsL ◂ ∀ A : ★ . ∀ n : Nat .
  List · (VecL · A n) ➔ List · (List · A)
  = Λ A . Λ n . cmapL · (VecL · A n) · (List · A)
    -(interCast · (List · A) · (λ xs : List · A . n ≃ length xs)) .

vs2lsL ◂ ∀ A : ★ . ∀ n : Nat .
  List · (Vec · A n) ➔ List · (List · A)
  = Λ A . Λ n . λ xs . us2lsL · A -n (vs2usL · A -n xs) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

vss2lssL ◂ ∀ A : ★ . ∀ n1 : Nat . ∀ n2 : Nat .
  List · (Vec · (Vec · A n1) n2) ➔ List · (List · (List · A))
  = Λ A . Λ n1 . Λ n2 .
  cmapL · (Vec · (Vec · A n1) n2) · (List · (List · A))
    -(compCast · (Vec · (Vec · A n1) n2) · (List · (Vec · A n1)) · (List · (List · A))
      -(cmapCastL · (Vec · A n1) · (List · A)
        -(v2lCast · A -n1))
      -(v2lCast · (Vec · A n1) -n2)) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
