import Nat.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Vec' ◂ ★ ➔ Nat ➔ ★ = λ A : ★ . λ n : Nat . ∀ X : Nat ➔ ★ .
  X zero ➔
  (∀ n : Nat . A ➔ X n ➔ X (suc n)) ➔
  X n .

nilV' ◂ ∀ A : ★ . Vec' · A zero =
  Λ A . Λ X . λ cN . λ cC . cN .

consV' ◂ ∀ A : ★ . ∀ n : Nat . A ➔ Vec' · A n ➔ Vec' · A (suc n) =
  Λ A . Λ n . λ x . λ xs . Λ X . λ cN . λ cC . cC -n x (xs · X cN cC) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

VecParam ◂ Π A : ★ . Π n : Nat . Vec' · A n ➔ ★ =
  λ A : ★ . λ n : Nat . λ xs' : Vec' · A n .
  ∀ X : Nat ➔ ★ . ∀ P : Π n : Nat . X n ➔ ★ .
  ∀ cN : X zero . ∀ cC : ∀ n : Nat . A ➔ X n ➔ X (suc n) .
  P zero cN ➔
  (∀ n : Nat . ∀ xs : X n . Π x : A . P n xs ➔ P (suc n) (cC -n x xs)) ➔
  P n (xs' · X cN cC) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Vec ◂ ★ ➔ Nat ➔ ★ = λ A : ★ . λ n : Nat .
  ι xs' : Vec' · A n . ι _ : (xs' nilV' consV' ≃ xs') . VecParam · A n xs' .

nilV ◂ ∀ A : ★ . Vec · A zero = Λ A .
  [ nilV' · A ,
  [ β{nilV' · A} ,
  Λ X . Λ P . Λ cN . Λ cC . λ pN . λ pC .
  pN ] ] .

consV ◂ ∀ A : ★ . ∀ n : Nat. A ➔ Vec · A n ➔ Vec · A (suc n) =
  Λ A . Λ n . λ x . λ xs .
  [ consV' · A -n x xs.1 , [ ρ+ xs.2.1 - β{consV' · A -n x xs.1} ,
  Λ X . Λ P . Λ cN . Λ cC . λ pN . λ pC .
  pC -n -(xs.1 · X cN cC) x (xs.2.2 · X · P -cN -cC pN pC) ] ] .

elimVec ◂ ∀ A : ★ . ∀ P : Π n : Nat . Vec · A n ➔ ★ .
  P zero (nilV · A) ➔
  (∀ n : Nat . ∀ xs : Vec · A n . Π x : A . P n xs ➔ P (suc n) (consV · A -n x xs)) ➔
  ∀ n : Nat . Π xs : Vec · A n . P n xs
  = Λ A . Λ P . λ pN . λ pC . Λ n . λ xs .
  ρ ς xs.2.1 -
  (xs.2.2 · (Vec · A) · P -(nilV · A) -(consV · A) pN pC) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

