module ProofReuse.InterSub.
-- Derive problematic distributivity rule
-- from Section 2.2 of
-- "Intersection Types and Computational Effects"
-- by Davies and Pfenning (2000)

Cast ◂ ★ ➔ ★ ➔ ★ =
  λ A : ★ . λ B : ★ .
  Π a : A . ι b : B . {b ≃ a} .

cast ◂ ∀ A : ★ . ∀ B : ★ . Cast · A · B ➾ A ➔ B =
  Λ A . Λ B . Λ c . λ a .
  φ (c a).2 - (c a).1 {a} .

interR ◂ ∀ A : ★ . ∀ B1 : ★ . ∀ B2 : ★ .
  Cast · A · B1 ➔
  Cast · A · B2 ➔
  Cast · A · (ι _ : B1 . B2)
  = Λ A . Λ B1 . Λ B2 . λ c1 . λ c2 . λ a .
  [ [ cast · A · B1 -c1 a , cast · A · B2 -c2 a ]
  , β{a}
  ] .

interL1 ◂ ∀ A1 : ★ . ∀ A2 : ★ . ∀ B : ★ .
  Cast · A1 · B ➔
  Cast · (ι _ : A1 . A2) · B
  = Λ A1 . Λ A2 . Λ B . λ c . λ a .
  [ cast · A1 · B -c a.1
  , β{a}
  ] .

interL2 ◂ ∀ A1 : ★ . ∀ A2 : ★ . ∀ B : ★ .
  Cast · A2 · B ➔
  Cast · (ι _ : A1 . A2) · B
  = Λ A1 . Λ A2 . Λ B . λ c . λ a .
  [ cast · A2 · B -c a.2
  , β{a}
  ] .

interDist ◂ ∀ A : ★ . ∀ B1 : ★ . ∀ B2 : ★ .
  Cast
    · ((ι _ : A ➔ B1 . A ➔ B2))
    · (A ➔ (ι _ : B1 . B2))
  = Λ A . Λ B1 . Λ B2 . λ f .
  [ λ a . [ f.1 a  , f.2 a ]
  , β{f}
  ] .
