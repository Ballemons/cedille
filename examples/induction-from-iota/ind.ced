cNat ◂ ★ = ∀ X : ★ . (X ➔ X) ➔ X ➔ X .
cZ ◂ cNat = Λ X . λ s . λ z . z .
cS ◂ cNat ➔ cNat = λ n . Λ X . λ s . λ z . s (n · X s z) .

Inductive ◂ cNat ➔ ★ = λ x : cNat . ∀ Q : cNat ➔ ★ . (∀ x : cNat . Q x ➔ Q (cS x)) ➔ Q cZ ➔ Q x.

Nat ◂ ★ = ι x : cNat . Inductive x.

Z ◂ Nat = [ cZ , Λ X . λ s . λ z . z ] .
S ◂ Nat ➔ Nat = λ n . [ cS n.1 , Λ P . λ s . λ z . s -n.1 (n.2 · P s z) ] .

Ind ◂ ∀ Q : Nat ➔ ★ . (∀ x : Nat . Q x ➔ Q (S x)) ➔ Q Z ➔ Π x : Nat . Q x =
  Λ Q . λ s . λ z . λ x .
    x.2 · (λ x : cNat . ∀ X : ★ . (∀ x' : Nat . (x ≃ x') ➾ Q x' ➔ X) ➔ X)
     (Λ x' . λ ih . Λ X . λ c . ih · X (Λ x'' . Λ e . λ u . c -(S x'') -(ρ e - β) (s -x'' u)))
     (Λ X . λ c . c -Z -β z) · (Q x) (Λ x' . Λ e . λ u . ρ e - u).
      
