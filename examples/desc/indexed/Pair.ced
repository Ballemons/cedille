%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

PairC ◂ Π A : ★ . (A ➔ ★) ➔ ★ = λ A : ★ . λ B : A ➔ ★ .
  ∀ X : ★ . (Π a : A . B a ➔ X) ➔ X.

pairC ◂ ∀ A : ★ . ∀ B : A ➔ ★ . Π a : A . B a ➔ PairC · A · B =
  Λ A . Λ B . λ a . λ b . Λ X . λ c . c a b .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

PairP ◂ Π A : ★ . Π B : A ➔ ★ . PairC · A · B ➔ ★ =
  λ A : ★ . λ B : A ➔ ★ . λ t : PairC · A · B .
  ∀ X : ★ . ∀ P : X ➔ ★ .
  ∀ c : Π a : A . B a ➔ X .
  Π p : Π a : A . Π b : B a . P (c a b) .
  P (t · X c) .

pairP ◂ ∀ A : ★ . ∀ B : A ➔ ★ .
  Π a : A . Π b : B a . PairP · A · B (pairC · A · B a b) =
  Λ A . Λ B . λ a . λ b . Λ X . Λ P . Λ c . λ p .
  p a b .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

PairR ◂ Π A : ★ . Π B : A ➔ ★ . PairC · A · B ➔ ★
  = λ A : ★ . λ B : A ➔ ★ . λ tC : PairC · A · B .
  tC pairC ≃ tC .

pairR ◂ ∀ A : ★ . ∀ B : A ➔ ★ .
  ∀ a : A . ∀ b : B a . PairR · A · B (pairC · A · B a b)
  = Λ A . Λ B . Λ a . Λ b . β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Pair ◂ Π A : ★ . (A ➔ ★) ➔ ★ = λ A : ★ . λ B : A ➔ ★ .
  ι t : (ι tC : PairC · A · B . PairP · A · B tC) . PairR · A · B t.1 .

Pair' ◂ ★ ➔ ★ ➔ ★ = λ A : ★ . λ B : ★ .
  Pair · A · (λ _ : A . B) .

mkPair ◂ ∀ A : ★ . ∀ B : A ➔ ★ .
  Π t : (ι tC : PairC · A · B . PairP · A · B tC) .
  PairR · A · B t.1 ➾ Pair · A · B =
  Λ A . Λ B . λ t . Λ q . [ t , ρ q - β{t} ] .

pair ◂ ∀ A : ★ . ∀ B : A ➔ ★ .
  Π a : A . Π b : B a . Pair · A · B
  = Λ A . Λ B . λ a . λ b . mkPair · A · B
  [pairC · A · B a b , pairP · A · B a b]
  -(pairR · A · B -a -b) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

elimPair ◂ ∀ A : ★ . ∀ B : A ➔ ★ .
  ∀ P : Pair · A · B ➔ ★ .
  Π p : Π a : A . Π b : B a . P (pair · A · B a b) .
  Π t : Pair · A · B . P t
  = Λ A . Λ B . Λ P . λ p . λ t .
  ρ ς t.2 -
  (t.1.2 · (Pair · A · B) · P -(pair · A · B) p) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

