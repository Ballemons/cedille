%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

PairC ◂ ★ ➔ ★ ➔ ★ = λ A : ★ . λ B : ★ .
  ∀ X : ★ . (A ➔ B ➔ X) ➔ X.

pairC ◂ ∀ A : ★ . ∀ B : ★ . A ➔ B ➔ PairC · A · B =
  Λ A . Λ B . λ a . λ b . Λ X . λ c . c a b .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

PairP ◂ Π A : ★ . Π B : ★ . PairC · A · B ➔ ★ =
  λ A : ★ . λ B : ★ . λ tC : PairC · A · B .
  ∀ X : ★ . ∀ P : X ➔ ★ .
  ∀ c : A ➔ B ➔ X .
  Π p : Π a : A . Π b : B . P (c a b) .
  P (tC · X c) .

pairP ◂ ∀ A : ★ . ∀ B : ★ .
  Π a : A . Π b : B . PairP · A · B (pairC · A · B a b) =
  Λ A . Λ B . λ a . λ b . Λ X . Λ P . Λ c . λ p .
  p a b .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

PairR ◂ Π A : ★ . Π B : ★ . PairC · A · B ➔ ★
  = λ A : ★ . λ B : ★ . λ tC : PairC · A · B .
  tC pairC ≃ tC .

pairR ◂ ∀ A : ★ . ∀ B : ★ .
  ∀ a : A . ∀ b : B . PairR · A · B (pairC · A · B a b)
  = Λ A . Λ B . Λ a . Λ b . β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Pair ◂ ★ ➔ ★ ➔ ★ = λ A : ★ . λ B : ★ .
  ι t : (ι tC : PairC · A · B . PairP · A · B tC) . PairR · A · B t.1 .

mkPair ◂ ∀ A : ★ . ∀ B : ★ .
  Π t : (ι tC : PairC · A · B . PairP · A · B tC) .
  PairR · A · B t.1 ➾ Pair · A · B =
  Λ A . Λ B . λ t . Λ q . [ t , ρ q - β{t} ] .

pair ◂ ∀ A : ★ . ∀ B : ★ .
  Π a : A . Π b : B . Pair · A · B
  = Λ A . Λ B . λ a . λ b . mkPair · A · B
  [pairC · A · B a b , pairP · A · B a b]
  -(pairR · A · B -a -b) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

elimPair ◂ ∀ A : ★ . ∀ B : ★ .
  ∀ P : Pair · A · B ➔ ★ .
  Π p : Π a : A . Π b : B . P (pair · A · B a b) .
  Π t : Pair · A · B . P t
  = Λ A . Λ B . Λ P . λ p . λ t .
  ρ ς t.2 -
  (t.1.2 · (Pair · A · B) · P -(pair · A · B) p) .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

