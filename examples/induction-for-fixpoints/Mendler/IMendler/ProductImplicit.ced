

cProductImplicit ◂ ★ ➔ ★ ➔ ★ = λ A : ★ . λ B : ★ . ∀ X : ★ . (A ➾ B ➔ X) ➔ X .

cpairImpl ◂ ∀ A : ★ . ∀ B : ★ . A ➾ B ➔ cProductImplicit · A · B = Λ A . Λ B . Λ a . λ b . Λ X . λ ca . ca -a b.

param-ProductImplicit ◂ Π A : ★ . Π B : ★ . cProductImplicit · A · B ➔ ★ =
  λ A : ★ . λ B : ★ . λ x : cProductImplicit · A · B .
    ∀ X : ★ . ∀ P : X ➔ ★ . ∀ pr : A ➾ B ➔ X . 
      (∀ a : A . Π b : B . P (pr -a b)) ➔ P (x · X pr).

ProductImplicit ◂ ★ ➔ ★ ➔ ★ = λ A : ★ . λ B : ★ . ι x : cProductImplicit · A · B . ι _ : x cpairImpl ≃ x . param-ProductImplicit · A · B x .

pairImpl ◂ ∀ A : ★ . ∀ B : ★ . A ➾ B ➔ ProductImplicit · A · B =
  Λ A . Λ B . Λ a . λ b . [ cpairImpl · A · B -a b , [β{cpairImpl b} , Λ X . Λ P . Λ pr . λ p . p -a b ]].

sndImpl ◂ ∀ A : ★ . ∀ B : ★ . ProductImplicit · A · B ➔ B =
  Λ A . Λ B . λ pr . pr.1 · B (Λ a . λ b . b) .

ind-ProductImplicit ◂ ∀ A : ★ . ∀ B : ★ . Π x : ProductImplicit · A · B .
          ∀ P : ProductImplicit · A · B ➔ ★ .
          (∀ a : A . Π b : B . P (pairImpl · A · B -a b)) ➔ 
          P x =
  Λ A . Λ B . λ x . Λ P . λ p .
    ρ ς x.2.1 - (x.2.2 · (ProductImplicit · A · B) · P -(pairImpl · A · B) p) .

