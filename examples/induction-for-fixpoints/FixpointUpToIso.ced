
import InductionAxiomatic.

FunctorEq ◂ (★ ➔ ★) ➔ (★ ➔ ★) ➔ ★ = λ F : ★ ➔ ★ . λ G : ★ ➔ ★ .
    ∀ Q : ★ . (Π fg : ∀ X : ★ . F · X ➔ G · X . Π gf : ∀ X : ★ . G · X ➔ F · X . (∀ fx : ∀ X : ★ . F · X . gf (fg fx) ≃ fx)
      ➔ (∀ gx : ∀ X : ★ . F · X . fg (gf gx) ≃ gx) ➔ Q) ➔ Q  .

FunctorFun1 ◂ (★ ➔ ★) ➔ (★ ➔ ★) ➔ ★ = λ F : ★ ➔ ★ . λ G : ★ ➔ ★ .
    ∀ X : ★ . F · X ➔ G · X .

FunctorFun2 ◂ (★ ➔ ★) ➔ (★ ➔ ★) ➔ ★ = λ F : ★ ➔ ★ . λ G : ★ ➔ ★ .
    ∀ X : ★ . G · X ➔ F · X .

FunctorEq1 ◂ Π F : ★ ➔ ★ . Π G : ★ ➔ ★ . FunctorFun1 · F · G ➔ FunctorFun2 · F · G ➔ ★ = λ F : ★ ➔ ★ . λ G : ★ ➔ ★ .
    λ fg : FunctorFun1 · F · G .  λ gf : FunctorFun2 · F · G . (∀ X : ★ . ∀ fx : F · X . gf (fg fx) ≃ fx)  .

FunctorEq2 ◂ Π F : ★ ➔ ★ . Π G : ★ ➔ ★ . FunctorFun1 · F · G ➔ FunctorFun2 · F · G ➔ ★ = λ F : ★ ➔ ★ . λ G : ★ ➔ ★ .
    λ fg : FunctorFun1 · F · G .  λ gf : FunctorFun2 · F · G . (∀ X : ★ . ∀ gx : G · X . fg (gf gx) ≃ gx)  .

cinfg ◂ ∀ F : ★ ➔ ★ . ∀ G : ★ ➔ ★ . FunctorFun2 · F · G ➔ Functor · F ➔ G · (C · F) ➔ C · F =
  Λ F . Λ G . λ gf . λ FF . λ gcf . cin · F FF  (gf · (C · F)  gcf)  .

coutfg ◂ ∀ F : ★ ➔ ★ . ∀ G : ★ ➔ ★ . FunctorFun1 · F · G ➔ Functor · F ➔ C · F ➔ G · (C · F) =
  Λ F . Λ G . λ fg . λ FF . λ cf . fg · (C · F) (cout · F FF cf)  .

cfoldfg ◂ ∀ F : ★ ➔ ★ . ∀ G : ★ ➔ ★ . FunctorFun1 · F · G  ➔  ∀ X : ★ . (G · X ➔ X) ➔ C · F ➔ X =
  Λ F .  Λ G . λ fg . Λ X . λ k . λ t . (t · X (λ fx . k (fg · X fx)))   .

infg ◂ ∀ F : ★ ➔ ★ .
       ∀ G : ★ ➔ ★ . FunctorFun2 · F · G ➔ 
       
     Π FF : Functor · F . ∀ IHType : 𝒌IHType · F .
     ∀ F' : (Π X : ★ . (X ➔ ★) ➔ ★) . 
     FunctorLaw2 · F FF ➾ 
     FunctorLaw1 · F FF ➾
     ∀ FFD : FtoSigFam · F · F' .
     FtoSigFamProp · F FF · F' FFD ➾
     ∀ H : SigFamToIH · F · F' FFD FF · IHType .
     SigFamToIHProp · F · F' FFD  FF · IHType H ➾
     G · (D · F FF · IHType) ➔
     D · F FF · IHType = Λ F . Λ G . λ gf . λ fmp . Λ IHType . Λ F' .  Λ h1 . Λ h2 . Λ FFD . Λ FF-eq-FFD . Λ H . Λ idH . λ x .
       in · F fmp · IHType · F' -h1 -h2 -FFD -FF-eq-FFD -H -idH (gf · (D · F fmp · IHType) x)   .


infg-thm ◂ ∀ F : ★ ➔ ★ .
       ∀ G : ★ ➔ ★ .
       Π fg : FunctorFun1 · F · G .
       Π gf : FunctorFun2 · F · G .
       FunctorEq1 · F · G fg gf ➔ 
       Π FF : Functor · F .
       ∀ X : ★ . 
       ∀ x : F · X .
       infg gf FF (fg x) ≃ in FF x = Λ F . Λ G . λ fg . λ gf . λ eq1 . λ FF . Λ X . Λ x . ε ρ (eq1 · X -x)  - β   .


inductionAxiomaticIso ◂ 
  ∀ F : ★ ➔ ★ . ∀ G : ★ ➔ ★ .
  Π gf : FunctorFun2 · F · G .
  Π fg : FunctorFun1 · F · G .
  FunctorEq1 · F · G fg gf ➔
  Π FF : Functor · F . ∀ IHType : 𝒌IHType · F .
  ∀ F' : 𝒌SigmaFamily . 
  ∀ Hom1F : FunctorLaw2 · F FF.
  ∀ Hom2F : FunctorLaw1 · F FF.
  ∀ FFD : FtoSigFam · F · F' .
  ∀ FF-eq-FFD : FtoSigFamProp · F FF · F' FFD .
  ∀ H : SigFamToIH · F · F' FFD FF · IHType .
  ∀ idH : SigFamToIHProp · F · F' FFD FF · IHType H .

  Π fff-ext : IHTypeElem · F · IHType .
  ∀ fff-ext-prf : IHTypeElemProp · F · IHType fff-ext .

  ∀ P : D · F FF · IHType ➔ ★ .
  (∀ x : G · (D · F FF · IHType) .
    IHType · (D · F FF · IHType) ·
         (Enr · (D · F FF · IHType) · P) (EnrElem · (D · F FF · IHType) · P) (EnrEq · (D · F FF · IHType) · P) (gf · (D · F FF · IHType) x)
     ➔ P (in · F FF · IHType · F'  -Hom1F -Hom2F -FFD -FF-eq-FFD -H -idH (gf · (D · F FF · IHType) x))) ➔
  Π x : D · F FF · IHType .
  P x = Λ F . Λ G . λ gf . λ fg . λ eq1. λ FF . Λ IHType . Λ F' . Λ h1 . Λ h2 . Λ FFD . Λ FF-eq-FFD . Λ H . Λ idH .
        λ fff-ext . Λ fff-ext-prf . Λ P . λ pk . λ x . inductionAxiomatic · F FF · IHType · F' -h1 -h2 -FFD -FF-eq-FFD -H -idH fff-ext -fff-ext-prf · P
	(Λ x' . λ ih . ρ ς (eq1 · (D · F FF · IHType)  -x') - (pk -(fg · (D · F FF · IHType) x') (ρ (eq1 · (D · F FF · IHType)  -x') - ih))    )
	x  . 
