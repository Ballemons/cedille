
cSumImplicit ◂ ★ ➔ ★ ➔ ★ 
 = λ A : ★. λ B : ★. 
 ∀ X : ★. (A ➾ X) ➔ (B ➔ X) ➔ X.

cinImpl1 ◂ ∀ A : ★. ∀ B : ★. A ➾ cSumImplicit · A · B 
 = Λ A. Λ B. Λ a. Λ X. λ ca. λ cb. ca -a.

cinImpl2 ◂ ∀ A : ★. ∀ B : ★. B ➔ cSumImplicit · A · B 
 = Λ A. Λ B. λ b. Λ X. λ ca. λ cb. cb b.

param-SumImplicit ◂ Π A : ★. Π B : ★. cSumImplicit · A · B ➔ ★
 = λ A : ★. λ B : ★. λ x : cSumImplicit · A · B.
 ∀ X : ★. ∀ P : X ➔ ★. ∀ ca : A ➾ X. ∀ cb : B ➔ X.
  (∀ a : A. P (ca -a)) ➔ (Π b : B. P (cb b)) ➔ P (x · X ca cb).

SumImplicit ◂ ★ ➔ ★ ➔ ★ 
 = λ A : ★. λ B : ★. 
 ι x : cSumImplicit · A · B. ι _ : x cinImpl1 cinImpl2 ≃ x. param-SumImplicit · A · B x.

inImpl1 ◂ ∀ A : ★. ∀ B : ★. A ➾ SumImplicit · A · B 
 = Λ A. Λ B. Λ a. 
 [ cinImpl1 · A · B -a 
 , [β{cinImpl1} , Λ X. Λ P. Λ ca. Λ cb. λ pa. λ pb. pa -a ]].

inImpl2 ◂ ∀ A : ★. ∀ B : ★. B ➔ SumImplicit · A · B 
 = Λ A. Λ B. λ b. 
 [ cinImpl2 · A · B b 
 , [β{cinImpl2 b} , Λ X. Λ P. Λ ca. Λ cb. λ pa. λ pb. pb b ]].

ind-SumImplicit ◂ ∀ A : ★. ∀ B : ★. Π x : SumImplicit · A · B.
 ∀ P : SumImplicit · A · B ➔ ★.
 (∀ a : A. P (inImpl1 · A · B -a)) ➔ (Π b : B. P (inImpl2 · A · B b)) ➔
 P x
 = Λ A. Λ B. λ x. Λ P. λ pa. λ pb.
 ρ ς x.2.1 - 
  (x.2.2 · (SumImplicit · A · B) · P -(inImpl1 · A · B) -(inImpl2 · A · B) pa pb).

caseImpl ◂ ∀ A : ★. ∀ B : ★. ∀ C : ★. SumImplicit · A · B ➔ (A ➔ C) ➔ (B ➔ C) ➔ C
 = Λ A. Λ B. Λ C. λ s. λ f. λ g. s.1 · C  f g.
