import recType.

module Hom(F : ★ ➔ ★){fm : RecFunctor · F}.

import sigma.

import Ind · F -fm.

IsHom : Π X1 : ★ . (Alg · C · X1) ➔
        Π X2 : ★ . (Alg · C · X2) ➔ 
        Π h : X1 ➔ X2 . ★ =
  λ X1 : ★ . λ alg1 : Alg · C · X1 .
  λ X2 : ★ . λ alg2 : Alg · C · X2 .
  λ h : X1 ➔ X2 .
    ∀ R : ★ . Cast · R · C ➾
    Π fr : F · R . { h (alg1 (foldU alg1) fr) ≃ alg2 (foldU alg2) fr }.

HomFold : ∀ X1 : ★ . ∀ alg1 : Alg · C · X1 . 
          ∀ X2 : ★ . ∀ alg2 : Alg · C · X2 .
          ∀ h : X1 ➔ X2 .
          IsHom · X1 alg1 · X2 alg2 h ➔
          ∀ R : ★ . Cast · R · C ➾
          Π r : R .
          { h (fold alg1 r) ≃ fold alg2 r } =
  Λ X1 . Λ alg1 . Λ X2 . Λ alg2 . Λ h . λ H .
  Λ R . Λ c . λ r . 
    θ<r> (Ind (cast -c r))
    [ β{λ ih . λ fr . H fr} ,
      Λ R . Λ reveal . λ ih . λ fr .
        (H · R -reveal fr)].

IdHom : ∀ X : ★ . ∀ alg : Alg · C · X .
        IsHom · X alg · X alg (λ x . x) =
   Λ X . Λ alg . Λ R . Λ _ . λ fr . β.

ComposeHom : ∀ X1 : ★ . ∀ alg1 : Alg · C · X1 .
             ∀ X2 : ★ . ∀ alg2 : Alg · C · X2 .
             ∀ X3 : ★ . ∀ alg3 : Alg · C · X3 .
             ∀ h1 : X1 ➔ X2 .
             ∀ h2 : X2 ➔ X3 .
             IsHom · X1 alg1 · X2 alg2 h1 ➔
             IsHom · X2 alg2 · X3 alg3 h2 ➔              
             IsHom · X1 alg1 · X3 alg3 (λ x . h2 (h1 x)) =
   Λ X1 . Λ alg1 .
   Λ X2 . Λ alg2 .
   Λ X3 . Λ alg3 .      
   Λ h1 . Λ h2 . λ H1 . λ H2 .
   Λ R . Λ c . λ fr . ρ+ (H1 · R -c fr) - ρ (H2 · R -c fr) - β.

WeakInitiality : ∀ X : ★ . ∀ alg : Alg · C · X .
                 IsHom · C (inAlg -fm) · X alg (fold -fm · X alg) =
  Λ X . Λ alg . 
  Λ R . Λ c . λ fr . β.

Initiality : ∀ X : ★ . ∀ alg : Alg · C · X .
             ∀ h : C ➔ X . 
             IsHom · C (inAlg -fm) · X alg h ➔
             Π c : C . { h c ≃ fold alg c } =
  Λ X . Λ alg . Λ h . λ hh . λ c .
    θ<c> (Ind c)
      [ β{λ rec . λ d . β} ,
        Λ R . Λ reveal . λ rec . λ d . ρ+ (hh · R -reveal d) - β ].
