import bool-thms.
import ctrm.
import extras.
import nat-thms/compare.

param-ctrm ◂ ctrm ➔ ★ = 
  λ x : ctrm . ∀ A : ★ . ∀ B : A ➔ ★ . ∀ R : (Π a : A . B a) ➔ ★ .
    ∀ app : Π a : A . B a ➔ B a ➔ B a .
    ∀ lam : Π a : A . (B a ➔ B a) ➔ B a.
    ∀ var : Π a : A . Nat ➔ B a .
      (∀ p1 : (Π a : A . B a) .
       ∀ p2 : (Π a : A . B a) . 
       R p1 ➔ R p2 ➔ R (λ a . app a (p1 a) (p2 a))) ➔
      (∀ f : Π a : A . B a ➔ B a .
       (∀ p : (Π a : A . B a) . R p ➔ R (λ a . f a (p a))) ➔ 
       R (λ a . lam a (f a))) ➔
      (Π n : Nat . R (λ a . var a n)) ➔
      R (λ a . x · (B a) (app a) (lam a) (var a)) .

trm ◂ ★ = ι x : ctrm . param-ctrm x .

param1-trm ◂ Π t : trm .
             ∀ A : ★ . ∀ R : A ➔ ★ .
             ∀ app : A ➔ A ➔ A.
             ∀ lam : (A ➔ A) ➔ A.
             ∀ var : Nat ➔ A.
               (∀ a1 : A . ∀ a2 : A .
                R a1 ➔ R a2 ➔ R (app a1 a2)) ➔
               (∀ f : A ➔ A .
                (∀ a : A . R a ➔ R (f a)) ➔
                R (lam f)) ➔
               (Π n : Nat . R (var n)) ➔
               R (t.1 · A app lam var) =
  λ t . Λ A . Λ R . Λ app . Λ lam . Λ var .
    λ pa . λ pl . λ pv .
      t.2 · True · (λ _ : True . A) · (λ p : True ➔ A . R (p triv))
        -(λ _ : True . app) -(λ _ : True . lam) -(λ _ : True . var)
        (Λ p1 . Λ p2 . λ u1 . λ u2 . (pa -(p1 triv) -(p2 triv) u1 u2))
        (Λ f . λ u . pl -(f triv) (Λ a . λ ua . u -(λ _ . a) ua))
        pv .

param2-t ◂ ctrm ➔ ★ = λ t : ctrm . 
             ∀ A : ★ . ∀ B : ★ . ∀ R : A ➔ B ➔ ★ .
             ∀ app1 : A ➔ A ➔ A.
             ∀ lam1 : (A ➔ A) ➔ A.
             ∀ var1 : Nat ➔ A.
             ∀ app2 : B ➔ B ➔ B.
             ∀ lam2 : (B ➔ B) ➔ B.
             ∀ var2 : Nat ➔ B.
               (∀ a1 : A . ∀ a2 : A . ∀ b1 : B . ∀ b2 : B .
                R a1 b1 ➔ R a2 b2 ➔ R (app1 a1 a2) (app2 b1 b2)) ➔
               (∀ f1 : A ➔ A . ∀ f2 : B ➔ B .
                (∀ a : A . ∀ b : B . R a b ➔ R (f1 a) (f2 b)) ➔
                R (lam1 f1) (lam2 f2)) ➔
               (Π n : Nat . R (var1 n) (var2 n)) ➔
               R (t · A app1 lam1 var1) (t · B app2 lam2 var2).

param2-trm ◂ Π t : trm . param2-t t.1
 =
 λ t . Λ A . Λ B . Λ R . Λ app1 . Λ lam1 . Λ var1 . Λ app2 . Λ lam2 . Λ var2 .
   λ pa . λ pl . λ pv .
     t.2 · Bool · (λ b : Bool . if-t b · A · B) · (λ p : Π b : Bool. if-t b · A · B . R (p tt) (p ff))
       -(λ b : Bool . θ b app1 app2) -(λ b : Bool . θ b lam1 lam2) -(λ b : Bool . θ b var1 var2)
         (Λ p1 . Λ p2 . λ u1 . λ u2 . pa -(p1 tt) -(p2 tt) -(p1 ff) -(p2 ff) u1 u2)
         (Λ f . λ u . pl -(f tt) -(f ff) (Λ a . Λ b . λ u' . u -(λ x . θ x a b) u'))
         pv .

param3-t ◂ ctrm ➔ ★ = λ t : ctrm . 
             ∀ A : ★ . ∀ B : ★ . ∀ C : ★ . ∀ R : A ➔ B ➔ C ➔ ★ .
             ∀ app1 : A ➔ A ➔ A.
             ∀ lam1 : (A ➔ A) ➔ A.
             ∀ var1 : Nat ➔ A.
             ∀ app2 : B ➔ B ➔ B.
             ∀ lam2 : (B ➔ B) ➔ B.
             ∀ var2 : Nat ➔ B.
             ∀ app3 : C ➔ C ➔ C.
             ∀ lam3 : (C ➔ C) ➔ C.
             ∀ var3 : Nat ➔ C.
               (∀ a1 : A . ∀ a2 : A . ∀ b1 : B . ∀ b2 : B .
                ∀ c1 : C . ∀ c2 : C . 
                R a1 b1 c1 ➔ R a2 b2 c2 ➔
                R (app1 a1 a2) (app2 b1 b2) (app3 c1 c2)) ➔
               (∀ f1 : A ➔ A . ∀ f2 : B ➔ B . ∀ f3 : C ➔ C .
                (∀ a : A . ∀ b : B . ∀ c : C . R a b c ➔
                R (f1 a) (f2 b) (f3 c)) ➔
                R (lam1 f1) (lam2 f2) (lam3 f3)) ➔
               (Π n : Nat . R (var1 n) (var2 n) (var3 n)) ➔
               R (t · A app1 lam1 var1) (t · B app2 lam2 var2)
                 (t · C app3 lam3 var3).

rec Three | athree : Three , bthree : Three , cthree : Three = 
  ∀ P : Three ➔ ★ .
    P athree ➔ P bthree ➔ P cthree ➔ P self
  with
    athree = Λ P . λ a . λ b . λ c . a ,
    bthree = Λ P . λ a . λ b . λ c . b ,
    cthree = Λ P . λ a . λ b . λ c . c
.

three-select-t ◂ Three ➔ ★ ➔ ★ ➔ ★ ➔ ★ =
  λ t : Three. ↑ X . t · (λ x : Three . X) : (☆ ➔ ☆ ➔ ☆ ➔ ☆) .

param3-trm ◂ Π t : trm . param3-t t.1
 =
 λ t . Λ A . Λ B . Λ C . Λ R .
 Λ app1 . Λ lam1 . Λ var1 .
 Λ app2 . Λ lam2 . Λ var2 .
 Λ app3 . Λ lam3 . Λ var3 .
   λ pa . λ pl . λ pv .
     t.2 · Three · (λ b : Three . three-select-t b · A · B · C)
     · (λ p : Π b : Three. three-select-t b · A · B · C .
          R (p athree) (p bthree) (p cthree))
       -(λ b : Three . θ b app1 app2 app3)
       -(λ b : Three . θ b lam1 lam2 lam3)
       -(λ b : Three . θ b var1 var2 var3)
         (Λ p1 . Λ p2 . λ u1 . λ u2 . 
           pa -(p1 athree) -(p2 athree)
              -(p1 bthree) -(p2 bthree)
              -(p1 cthree) -(p2 cthree)
              u1 u2)
         (Λ f . λ u .
           pl -(f athree) -(f bthree) -(f cthree)
             (Λ a . Λ b . Λ c . λ u' . u -(λ x . θ x a b c) u'))
         pv .

test = free-in-app.

free-maxvar ◂ Π t : trm . ∀ n : Nat . ∀ s : Nat . ∀ q : Nat ➔ Nat .
                (q n ≃ s) ➔ Free-in t.1 n ➔ Lte s (t.1 · Nat max (λ f . f Z) q) =
  λ t . Λ n . Λ s . Λ q . λ uq . 
    param2-trm t · (Nat ➔ Bool) · Nat · (λ f : Nat ➔ Bool . λ m : Nat . (f n ≃ tt) ➔ Lte s m)
       -free-in-app -free-in-lam -free-in-var -max -(λ f . f Z) -q
       (Λ a1 . Λ a2 . Λ b1 . Λ b2 . λ ih1 . λ ih2 . λ u .
          ρ (χ (Lte s (max b1 b2)) -
             (θ+ (a1 n) (λ v . Lte-trans s b1 (max b1 b2) (ih1 v) (Lte-max1 b1 b2))
                (λ v . θ+ (a2 n) (λ vv . Lte-trans s b2 (max b1 b2) (ih2 vv) (Lte-max2 b1 b2)) (λ vv. δ (ρ+ v - ρ+ vv - u))))) - β)
       (Λ f1 . Λ f2 . λ ih . λ uf . ih -(λ x . ff) -Z (λ u . δ u) uf)
       (λ n' . λ u . ρ ς (eqnat-eq n n' (ρ (eqnat-sym n n') - u)) - ρ uq - ρ (Lte-refl s) - β) .

free-ext-thm ◂ Π x : trm . ∀ A : ★ . ∀ app : A ➔ A ➔ A . ∀ lam : (A ➔ A) ➔ A . ∀ s1 : Nat ➔ A . ∀ s2 : Nat ➔ A .
                (∀ f1 : A ➔ A . ∀ f2 : A ➔ A . (∀ a : A . f1 a ≃ f2 a) ➔ lam f1 ≃ lam f2) ➔ 
                (Π n : Nat . Free-in x.1 n ➔ s1 n ≃ s2 n) ➔ x app lam s1 ≃ x app lam s2 =
  λ x . Λ A . Λ app . Λ lam . Λ s1 . Λ s2 . λ e . 
    param3-trm x · (Nat ➔ Bool) · A · A · (λ fi : Nat ➔ Bool . λ i1 : A . λ i2 : A . (Π n : Nat . (fi n ≃ tt) ➔ s1 n ≃ s2 n) ➔ i1 ≃ i2)
      -free-in-app -free-in-lam -free-in-var -app -lam -s1 -app -lam -s2
      (Λ a1 . Λ a2 . Λ b1 . Λ b2 . Λ c1 . Λ c2 . λ ih1 . λ ih2 . λ q .
        ρ (ih1 (λ n . λ v . q n (ρ+ v - β))) - ρ (ih2 (λ n . λ v . q n (ρ+ v - ρ (Bool-tt-tt (a1 n)) - β))) - β)
      (Λ f1 . Λ f2 . Λ f3 . λ ih . λ q . e -f2 -f3 (Λ a . ih -(λ x . ff) -a -a (λ _ . β) q))
      (λ n . λ u . u n (eqnat-refl n))
      .
      
rebuilding-trm ◂ Π t : trm . t capp clam cvar ≃ t =
  λ t . 
    ω (Λ A : ★ . ω (Λ a : A ➔ A ➔ A . ω (Λ l : (A ➔ A) ➔ A . ω (Λ v : Nat ➔ A .
      χ (t capp clam cvar a l v ≃ t a l v) -
        (param2-trm t · ctrm · A
          · (λ t : ctrm . λ q : A . t a l v ≃ q)
          -capp -clam -cvar
          -a -l -v
          ●
          (Λ f1 . Λ f2 . λ ih . 
            ρ+ (χ (clam-abs f1 a l v ≃ f2) -
                   (ω (Λ x : A .
                      χ (clam-abs f1 a l v x ≃ f2 x) -
                         ●))) - β)
          ●))))).
