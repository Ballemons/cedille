import ctors-lam.
import ind.
import inv.
import nat-thms/compare.
import sftrm.
import trms.

subst ◂ trm ➔ (Nat ➔ trm) ➔ trm = λ t . λ s . t.1.1.1 · trm app lam s . 

subst-thm ◂ Π t : trm . Π s : Nat ➔ trm . subst t s ≃ λ a . λ l . λ v . t a l (λ n . s n a l v) =
  λ t . λ s . t.2 -(λ n . (s n).1) .

closed ◂ trm ➔ Bool =
  λ t . t.1.1.1 · Bool
    [closed-app = and]
    [closed-lam = λ r . r tt]
    [closed-var = λ n . ff].

is-lam ◂ trm ➔ Bool = λ t . t.1.1.1 · Bool
                              [is-lam-app = λ _ . λ _ . ff]
                              [is-lam-lam = λ _ . tt]
                              [is-lam-var = λ _ . ff] .

is-app ◂ trm ➔ Bool = λ t . t.1.1.1 · Bool
       	       	      	    [is-app-app = λ _ . λ _ . tt]
			    [is-app-lam = λ _ . ff]
			    [is-app-var = λ _ . ff] .

is-lam-subst ◂ Π t : trm . Π s : Nat ➔ trm .
               (is-lam t ≃ tt) ➔ (is-lam (subst t s) ≃ tt) =
  λ t . λ s . λ h .
    ρ (subst-thm t s) -
    χ ( (is-lam (λ a . λ l . λ v . t a l (λ n . (s n a l v) ) ) ≃ tt ) ) -
    ε (param2-trm t · Bool · Bool · (λ a : Bool . λ b : Bool . (a ≃ tt) ➔ b ≃ tt )
    -is-lam-app -is-lam-lam -is-lam-var
    -is-lam-app -is-lam-lam -(λ n . (s n).1.1.1 · Bool is-lam-app is-lam-lam is-lam-var )
    (Λ _ . Λ _ . Λ _ . Λ _ . λ _ . λ _ . λ h3 . (δ h3))
    ( Λ _ . Λ _ . λ _ . λ _ . ε β )
    ( λ n . λ h1 . δ h1 )
    h ) .

is-app-subst ◂ Π t : trm . Π s : Nat ➔ trm .
	     (is-app t ≃ tt) ➔ (is-app (subst t s) ≃ tt) =
  λ t . λ s . λ h .
    ρ (subst-thm t s) -
    χ ( (is-app (λ a . λ l . λ v . t a l (λ n . (s n a l v) ) ) ≃ tt ) ) -
    ε (param2-trm t · Bool · Bool · (λ a : Bool . λ b : Bool . (a ≃ tt) ➔ b ≃ tt )
    -is-app-app -is-app-lam -is-app-var
    -is-app-app -is-app-lam -(λ n . (s n).1.1.1 · Bool is-app-app is-app-lam is-app-var )
    (Λ _ . Λ _ . Λ _ . Λ _ . λ _ . λ _ . λ h3 . β)
    ( Λ _ . Λ _ . λ _ . λ h2 . δ h2 )
    ( λ _ . λ h1 . δ h1 )
    h ) .



subst-closed-one-omega ◂ Π t : trm . ∀ s1 : Nat ➔ trm . ∀ s2 : Nat ➔ trm .
		      (closed t ≃ tt) ➔
		      subst t s1 ≃ subst t s2 =
  λ t . Λ s1 . Λ s2 . λ hc .
    (param3-trm t · Bool · trm · trm · (λ b : Bool . λ x : trm . λ y : trm . ( b ≃ tt ) ➔ x ≃ y)
    -closed-app -closed-lam -closed-var
    -app -lam -s1
    -app -lam -s2
    (Λ a1 . Λ a2 . Λ_.Λ_.Λ_.Λ_. λ h1 . λ h2 . λ h3 .
       ρ (h1 (andE1 a1 a2 h3) ) -
       ρ (h2 (andE2 a1 a2 h3) ) -
       β )
    (Λ f1 . Λ f2 . Λ f3 . λ h1 . λ h2 .
       ρ (χ (f2 ≃ f3) -
       	  ε (ω ((Λ r : trm . (h1 -tt -r -r (λ_. β) h2 ))))) - %looks like we need extensionality here...
       β )
    (λ_. λ h . δ h )
    hc).

subst-closed ◂ Π t : trm . ∀ s1 : Nat ➔ trm . ∀ s2 : Nat ➔ trm .
               (closed t ≃ tt) ➔
               subst t s1 ≃ subst t s2 =
  λ t . Λ s1 . Λ s2 . λ c .
    ρ (subst-thm t s1) -
    ρ (subst-thm t s2) - 
     ω (Λ X : ★ .
     ω (Λ a : X ➔ X ➔ X .
     ω (Λ l : (X ➔ X) ➔ X .
     ω (Λ v : Nat ➔ X .
        χ (t a l (λ n . s1 n a l v) ≃ t a l (λ n . s2 n a l v)) -
          ((param3-trm t · Bool · X · X
              · (λ b : Bool. λ x : X . λ y : X . (b ≃ tt) ➔ x ≃ y)
              -closed-app -closed-lam -closed-var
              -a -l -(λ n . (s1 n).1.1.1 · X a l v)
              -a -l -(λ n . (s2 n).1.1.1 · X a l v)            
              (Λ a1 . Λ a2 . Λ b1 . Λ b2 . Λ c1 . Λ c2 .
                λ ih1. λ ih2. λ ca .
                  ρ (ih1 (andE1 a1 a2 ca)) - 
                  ρ (ih2 (andE2 a1 a2 ca)) - β)                  
              (Λ f1 . Λ f2 . Λ f3 . λ ih . λ cl .
                ρ (χ (f2 ≃ f3) -
                   ω (Λ x : X .
                      χ (f2 x ≃ f3 x) -
                       (ih -tt -x -x (λ _ . β) cl))) - β)
              (λ n . λ u . δ u))
            c))))) . 

