module TpMod.

import recType.
import cast.
import top.

baseU ◂ Top = β{λ a . λ f . a}.
arrowU ◂ Top = β{λ p1 . λ n1 . λ p2 . λ n2 . λ a . λ f . f p1 (n1 a f) p2 (n2 a f)} .

TpF ◂ ★ ➔ ★ =
  λ TpC : ★ .
    ι x : ∀ X : ★ .
      Π z : X .
      (ι s : Top . Π p1 : TpC . Π x1 : X . {p1 z s ≃ x1} ➾ Π p2 : TpC . Π x2 : X . {p2 z s ≃ x2} ➾ X) ➔
      X .
    {x baseU arrowU ≃ x} .

TpFmap ◂ RecFunctor · TpF =
  Λ X . Λ Y . λ c .
    [ λ n . [ Λ Z . λ z . λ s .
                n.1 · Z z [ s.1 , λ p1 . λ x1 . Λ e1 . λ p2 . λ x2 . Λ e2 .
                            s.2 (cast · X · Y -c p1) x1 -e1 (cast · X · Y -c p2) x2 -e2],
                ρ+ n.2 - β{n} ] ,
              β{λ x . x}]. 
 
TpC ◂ ★ = Rec · TpF . 

TpFold ◂ TpF · TpC ➔ TpC =
  λ x . cast · (TpF · TpC) · TpC -(recFold · TpF -TpFmap) x .

TpUnfold ◂ TpC ➔ TpF · TpC =
  λ x . cast · TpC · (TpF · TpC) -(recUnfold · TpF -TpFmap) x .

TpUnfoldSimple ◂ TpC ➔ ∀ X : ★ . Π z : X . (Π p1 : TpC . Π x1 : X . Π p2 : TpC . Π x2 : X . X) ➔ X =
  λ t . Λ X . λ z . λ s . (TpUnfold t).1 · X z [ β{s} , λ p1 . λ x1 . Λ e1 . λ p2 . λ x2 . Λ e2 . s p1 x1 p2 x2] . 

TpUnfoldIter ◂ TpC ➔ ∀ X : ★ . Π z : X . (Π x1 : X . Π x2 : X . X) ➔ X =
  λ t . Λ X . λ z . λ s . (TpUnfoldSimple t) · X z (λ _ . λ x1 . λ _ . λ x2 . s x1 x2) . 

baseC ◂ TpC = TpFold [Λ X . λ a . λ f . a , β{baseU}].

arrowC ◂ Π p1 : TpC . Π n1 : TpC . {p1 ≃ n1} ➾ Π p2 : TpC . Π n2 : TpC . {p2 ≃ n2} ➾ TpC =
  λ p1 . λ n1 . Λ e1 . λ p2 . λ n2 . Λ e2 .
    TpFold [Λ X . λ a . λ f .
               f.2 p1 ((TpUnfold n1).1 · X a f) -(ρ e1 - β)
                 p2 ((TpUnfold n2).1 · X a f) -(ρ e2 - β) ,
             ρ+ (TpUnfold n1).2 - ρ+ (TpUnfold n2).2 - β{arrowU p1 n1 p2 n2}].

TpP ◂ TpC ➔ ★ =
  λ n : TpC . ∀ X : ★ . ∀ P : X ➔ ★ .
    ∀ z : X . P z ➔
    ∀ s : ι s : Top . Π p : TpC . Π x : X . {p z s ≃ x} ➾ Π p : TpC . Π x : X . {p z s ≃ x} ➾ X .
      (Π n1 : TpC. ∀ x1 : X . ∀ e1 : {n1 z s ≃ x1} . P x1 ➔
       Π n2 : TpC. ∀ x2 : X . ∀ e2 : {n2 z s ≃ x2} . P x2 ➔ 
        P (s.2 n1 x1 -e1 n2 x2 -e2)) ➔
    P ((TpUnfold n).1 · X z s).

baseP ◂ TpP baseC = Λ X . Λ P . Λ z . λ pz . Λ s . λ ps . pz .

arrowP ◂ Π n : TpC . ∀ x : TpC . ∀ e : {n ≃ x} . TpP x ➔
         Π n2 : TpC . ∀ x2 : TpC . ∀ e2 : {n2 ≃ x2} . TpP x2 ➔ 
           TpP (arrowC n x -e n2 x2 -e2) =
  λ n . Λ x . Λ e . λ px . λ n2 . Λ x2 . Λ e2 . λ px2 .
  Λ X . Λ P . Λ z . λ pz . Λ s . λ ps .
    ps n -((TpUnfold x).1 · X z s) -(ρ e - β) (px · X · P -z pz -s ps)
       n2 -((TpUnfold x2).1 · X z s) -(ρ e2 - β) (px2 · X · P -z pz -s ps).

Tp ◂ ★ = ι x : TpC . TpP x.

base ◂ Tp = [ baseC , baseP].
arrowi ◂ Π p : TpC . Π n : Tp . {p ≃ n} ➾ Π p : TpC . Π n : Tp . {p ≃ n} ➾ Tp =
  λ p . λ n . Λ e . λ p2 . λ n2 . Λ e2 . [ arrowC p n.1 -e p2 n2.1 -e2 , arrowP p -n.1 -e n.2 p2 -n2.1 -e2 n2.2 ].

elimTpi ◂
  Π n : Tp . 
  ∀ P : Tp ➔ ★ .
  P base ➔
  (Π p : TpC . ∀ n : Tp . ∀ e : {p ≃ n} . P n ➔ Π p' : TpC . ∀ n' : Tp . ∀ e' : {p' ≃ n'} . P n' ➔ P (arrowi p n -e p' n' -e')) ➔
  P n
=
  λ n . Λ P . λ pZ . λ pS . 
   ρ ς (TpUnfold n.1).2 -
     (n.2 · Tp · P -base pZ
       -[β{arrowC},(λ p . λ n . Λ e . λ p' . λ n' . Λ e' . arrowi p n -(ρ ς (TpUnfold p).2 - e) p' n' -(ρ ς (TpUnfold p').2 - e'))]
       (λ n . Λ x . Λ e . λ px . λ n' . Λ x' . Λ e' . λ px'. pS n -x -(ρ ς (TpUnfold n).2 - e) px n' -x' -(ρ ς (TpUnfold n').2 - e') px' ) ) .

arrow ◂ Tp ➔ Tp ➔ Tp = λ Ta . λ Tb . arrowi Ta.1 Ta -β Tb.1 Tb -β.

elimTp ◂
  Π n : Tp . 
  ∀ P : Tp ➔ ★ .
  P base ➔
  (Π Ta : Tp . Π Tb : Tp . P Ta ➔ P Tb ➔ P (arrow Ta Tb)) ➔
  P n
=
  λ n . Λ P . λ pz . λ ps .
    elimTpi n · P pz
      (λ p . Λ n . Λ e . λ pn . λ p' . Λ n' . Λ e' . λ pn' .
        ρ ς e - ρ ς e' - (ps (changeType · TpC · Tp p -n -e) (changeType · TpC · Tp p' -n' -e') (ρ e - pn) (ρ e' - pn'))) .

