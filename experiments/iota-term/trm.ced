import ctrm.
import extras.
import nat-thms/compare.

param-ctrm ◂ ctrm ➔ ★ = 
  λ x : ctrm . ∀ A : ★ . ∀ B : A ➔ ★ . ∀ R : (Π a : A . B a) ➔ ★ .
    ∀ app : Π a : A . B a ➔ B a ➔ B a .
    ∀ lam : Π a : A . (B a ➔ B a) ➔ B a.
    ∀ var : Π a : A . Nat ➔ B a .
      (∀ p1 : (Π a : A . B a) .
       ∀ p2 : (Π a : A . B a) . 
       R p1 ➔ R p2 ➔ R (λ a . app a (p1 a) (p2 a))) ➔
      (∀ f : Π a : A . B a ➔ B a .
       (∀ p : (Π a : A . B a) . R p ➔ R (λ a . f a (p a))) ➔ 
       R (λ a . lam a (f a))) ➔
      (Π n : Nat . R (λ a . var a n)) ➔
      R (λ a . x · (B a) (app a) (lam a) (var a)) .

ptrm ◂ ★ = ι x : ctrm . param-ctrm x .

param1-ptrm ◂ Π t : ptrm .
             ∀ A : ★ . ∀ R : A ➔ ★ .
             ∀ app : A ➔ A ➔ A.
             ∀ lam : (A ➔ A) ➔ A.
             ∀ var : Nat ➔ A.
               (∀ a1 : A . ∀ a2 : A .
                R a1 ➔ R a2 ➔ R (app a1 a2)) ➔
               (∀ f : A ➔ A .
                (∀ a : A . R a ➔ R (f a)) ➔
                R (lam f)) ➔
               (Π n : Nat . R (var n)) ➔
               R (t.1 · A app lam var) =
  λ t . Λ A . Λ R . Λ app . Λ lam . Λ var .
    λ pa . λ pl . λ pv .
      t.2 · True · (λ _ : True . A) · (λ p : True ➔ A . R (p triv))
        -(λ _ : True . app) -(λ _ : True . lam) -(λ _ : True . var)
        (Λ p1 . Λ p2 . λ u1 . λ u2 . (pa -(p1 triv) -(p2 triv) u1 u2))
        (Λ f . λ u . pl -(f triv) (Λ a . λ ua . u -(λ _ . a) ua))
        pv .

param2-t ◂ ctrm ➔ ★ = λ t : ctrm . 
             ∀ A : ★ . ∀ B : ★ . ∀ R : A ➔ B ➔ ★ .
             ∀ app1 : A ➔ A ➔ A.
             ∀ lam1 : (A ➔ A) ➔ A.
             ∀ var1 : Nat ➔ A.
             ∀ app2 : B ➔ B ➔ B.
             ∀ lam2 : (B ➔ B) ➔ B.
             ∀ var2 : Nat ➔ B.
               (∀ a1 : A . ∀ a2 : A . ∀ b1 : B . ∀ b2 : B .
                R a1 b1 ➔ R a2 b2 ➔ R (app1 a1 a2) (app2 b1 b2)) ➔
               (∀ f1 : A ➔ A . ∀ f2 : B ➔ B .
                (∀ a : A . ∀ b : B . R a b ➔ R (f1 a) (f2 b)) ➔
                R (lam1 f1) (lam2 f2)) ➔
               (Π n : Nat . R (var1 n) (var2 n)) ➔
               R (t · A app1 lam1 var1) (t · B app2 lam2 var2).

param2-ptrm ◂ Π t : ptrm . param2-t t.1
 =
 λ t . Λ A . Λ B . Λ R . Λ app1 . Λ lam1 . Λ var1 . Λ app2 . Λ lam2 . Λ var2 .
   λ pa . λ pl . λ pv .
     t.2 · Bool · (λ b : Bool . if-t b · A · B) · (λ p : Π b : Bool. if-t b · A · B . R (p tt) (p ff))
       -(λ b : Bool . θ b app1 app2) -(λ b : Bool . θ b lam1 lam2) -(λ b : Bool . θ b var1 var2)
         (Λ p1 . Λ p2 . λ u1 . λ u2 . pa -(p1 tt) -(p2 tt) -(p1 ff) -(p2 ff) u1 u2)
         (Λ f . λ u . pl -(f tt) -(f ff) (Λ a . Λ b . λ u' . u -(λ x . θ x a b) u'))
         pv .

free-maxvar ◂ Π t : ptrm . ∀ n : Nat . ∀ s : Nat . ∀ q : Nat ➔ Nat .
                (q n ≃ s) ➔ Free-in t.1 n ➔ Lte s (t.1 · Nat max (λ f . f Z) q) =
  λ t . Λ n . Λ s . Λ q . λ uq . 
    param2-ptrm t · Bool · Nat · (λ f : Bool . λ m : Nat . (f ≃ tt) ➔ Lte s m)
       -or -(λ f . f ff) -(eqnat n) -max -(λ f . f Z) -q
       (Λ a1 . Λ a2 . Λ b1 . Λ b2 . λ ih1 . λ ih2 . λ u .
          ρ (χ (Lte s (max b1 b2)) -
             (θ+ a1 (λ v . Lte-trans s b1 (max b1 b2) (ih1 v) (Lte-max1 b1 b2))
                (λ v . θ+ a2 (λ vv . Lte-trans s b2 (max b1 b2) (ih2 vv) (Lte-max2 b1 b2)) (λ vv. δ (ρ v - ρ vv - u))))) - β)
       (Λ f1 . Λ f2 . λ ih . λ uf . ih -ff -Z (λ u . δ u) uf)
       (λ n' . λ u . ρ ς (eqnat-eq n n' u) - ρ uq - ρ (Lte-refl s) - β) .

ptrme ◂ ★ = ι x : ptrm .
              ∀ A : ★ . ∀ app : A ➔ A ➔ A . ∀ lam : (A ➔ A) ➔ A . ∀ s1 : Nat ➔ A . ∀ s2 : Nat ➔ A .
                (Π n : Nat . Free-in x.1 n ➔ s1 n ≃ s2 n) ➾ x app lam s1 ≃ x app lam s2 .

trm ◂ ★ = ι x : ptrme .
                ∀ s : Nat ➔ ptrme . csubst s x ≃ λ a . λ l . λ v . x a l (λ n . s n a l v) .

param2-trm ◂ Π t : trm . param2-t t.1.1.1 =
               λ t . param2-ptrm t.1.1 .
