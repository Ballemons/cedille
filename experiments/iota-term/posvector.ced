module PosVector.

import nat.
import natExtras.
import product.
import sum.

PosVector ◂ (Nat ➔ ★) ➔ Nat ➔ ★ =
  λ T : Nat ➔ ★ . λ n : Nat . 
  ∀ X : Nat ➔ ★ .
  X Z ➔
  (Π m : Nat . T m ➔ X m ➔ X (S m)) ➔
  X n.

PVNil ◂ ∀ T : Nat ➔ ★ . PosVector · T Z =
  Λ T . Λ X . λ nil . λ cons . nil .

PVCons ◂ ∀ T : Nat ➔ ★ . Π n : Nat . T n ➔ PosVector · T n ➔ PosVector · T (S n) =
  Λ T . λ n . λ h . λ t . Λ X . λ nil . λ cons .
    cons n h (t · X nil cons).

PVfromFun ◂ ∀ T:Nat➔★.Π n:Nat .
            (Π m:Nat .Lt m n➔T m) ➔
            PosVector·T n =
  Λ T .λ n .λ f . (NatInd n ·(λ m:Nat .Lte m n➔PosVector·T m)
    (λ _ .PVNil·T)
    (λ m .λ p .λ l .[l'=Lt-Lte m n l]- PVCons·T m (f m l') (p (Lte-Lt m n l'))))
    (Lten n) .



FunfromPV ◂ ∀ T:Nat➔★.Π n:Nat .
            PosVector·T n ➔
	    Π m:Nat .
	    Lt m n ➔
	    T m =
  Λ T .λ n .λ v .λ m .λ l .
    (v·(λ n:Nat .Lt m n➔T m)
       (λ l .(lt-Z m l)·(T m))
       (λ p .λ tp .λ ih .λ l .ind-Sum·(Lt m p)·{m≃p} (Lt-S m p l) ·(λ s:Sum·(Lt m p)·{m≃p} . T m) (λ l . ih l) (λ e .ρ e -tp)))
       l.

{-Tests-}
Nat'◂Nat➔★=λ _:Nat .Nat.

testa◂Nat=S Z.
testc◂Nat=Z.
testb◂Nat=S (S Z).

{-    PVfromFun
-}

example0◂PosVector·Nat' (S (S (S Z)))=PVfromFun·Nat' (S (S (S Z))) (λ _ .λ _ .testa).

lookup-example0-head◂{FunfromPV·Nat' (S (S (S Z))) example0 (S (S Z)) (lt-nSn (S (S Z)))≃testa}=β.
lookup-example0-mid◂{FunfromPV·Nat'  (S (S (S Z))) example0 (S Z) (lt-nSSn (S Z))≃testa}=β.
lookup-example0-last◂{FunfromPV·Nat' (S (S (S Z))) example0 Z (lt-nSSSn Z)≃testa}=β.

{-    FunfromPV
      [ testa , testb,  testc ]
          T2     , T1    , T0
-}


example1◂PosVector·Nat' (S (S (S Z)))=
  PVCons·Nat' (S (S Z)) testa
   (PVCons·Nat' (S Z)   testb
     (PVCons·Nat' Z     testc
       (PVNil·Nat'))).

lookup-example1-head◂{FunfromPV·Nat' (S (S (S Z))) example1 (S (S Z)) (lt-nSn (S (S Z)))≃testa}=β.
lookup-example1-mid◂{FunfromPV·Nat' (S (S (S Z)))  example1 (S Z) (lt-nSSn (S Z))≃testb}=β.
lookup-example1-last◂{FunfromPV·Nat' (S (S (S Z))) example1 Z (lt-nSSSn Z)≃testc}=β.
