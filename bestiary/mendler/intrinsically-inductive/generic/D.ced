import cast.
import recType.
import is.
import top.
import true.

import C.

PrfAlg ◂ Π F : ★ ➔ ★ . RecFunctor · F ➔ ★ ➔ Π U : ★ . (F · U ➔ U) ➔ (U ➔ ★)➔ ★ =
  λ F : ★ ➔ ★ . λ fm : RecFunctor · F . λ D : ★ . λ U : ★ . λ inu : F · U ➔ U . λ P : U ➔ ★ .
    ι alg : Top .
    ∀ R : ★ . 
    Π pred : is · (R ➔ F · R) outU .
    ∀ reveal : Cast · R · U.    
    ∀ revealh : Cast · R · D .
    ∀ p' : F · U .
      is · (Π r : R . P (cast · R · U -reveal r)) (foldU β{alg}) ➔
      Π p : is · (ι _ : F · R . F · D) β{p'}.
      P (inu p') .

Inductive ◂ Π F : ★ ➔ ★ . RecFunctor · F ➔ ★ ➔ Π U : ★ . (F · U ➔ U) ➔ U ➔ ★ =
  λ F : ★ ➔ ★ . λ fm : RecFunctor · F . λ D : ★ . λ U : ★ . λ inu : F · U ➔ U . λ d : U.
    ∀ P : U ➔ ★ .
      PrfAlg · F fm · D · U inu · P ➔
      P d .


DF ◂ Π F : ★ ➔ ★ . RecFunctor · F ➔ ★ ➔ ★ =
  λ F : ★ ➔ ★ . λ fm : RecFunctor · F . 
  λ D : ★ . ι x : C · F . Inductive · F fm · D · (C · F) (in' · F -fm) x .

D ◂ Π F : ★ ➔ ★ . RecFunctor · F ➔ ★ = 
  λ F : ★ ➔ ★ . λ fm : RecFunctor · F . Rec · (DF · F fm).

DFunctor ◂ ∀ F : ★ ➔ ★ . Π fm : RecFunctor · F . RecFunctor · (DF · F fm) =
  Λ F . λ fm . Λ X . Λ Y . λ c .
    [ λ x .
       [ x.1 , Λ P . λ p .
               x.2 · P 
                 (Λ R . λ pred . Λ reveal1 . Λ reveal2 . Λ p' . λ eval . λ q .
                   p · R pred -reveal1 -(castCompose · R · X · Y reveal2 c) -p' eval
                     [ [ q.1.1 , cast · (F · X) · (F · Y) -(fm · X · Y c) q.1.2] , q.2])] , β] .

dFold ◂ ∀ F : ★ ➔ ★ . ∀ fm : RecFunctor · F . DF · F fm · (D · F fm) ➔ D · F fm =
  Λ F . Λ fm .
    cast · (DF · F fm · (D · F fm)) · (D · F fm) -(recFold · (DF · F fm) -(DFunctor · F fm)) . 

dUnfold ◂ ∀ F : ★ ➔ ★ . ∀ fm : RecFunctor · F . D · F fm ➔ DF · F fm · (D · F fm) =
  Λ F . Λ fm .
    cast · (D · F fm) · (DF · F fm · (D · F fm)) -(recUnfold · (DF · F fm) -(DFunctor · F fm)) . 

out ◂ ∀ F : ★ ➔ ★ . ∀ fm : RecFunctor · F . D · F fm ➔ F · (D · F fm) =
  Λ F . Λ fm . λ x .
    (dUnfold · F -fm x).2 · (λ _ : (C · F) . (F · (D · F fm)))
      (Λ R . λ pred . Λ reveal . Λ revealh . Λ p' . λ eval . λ d . d.1.2) .

confirmOut ◂ outU ≃ out = β{out}.

fold ◂ ∀ F : ★ ➔ ★ . ∀ fm : RecFunctor · F.
       ∀ P : (C · F) ➔ ★ .
         PrfAlg · F fm · (D · F fm) · (C · F) (in' · F -fm) · P ➔
         Π x : D · F fm .
         P (dUnfold · F -fm x).1 = 
      Λ F . Λ fm . Λ P . λ palg . λ x . (dUnfold · F -fm x).2 · P palg.

in ◂ ∀ F : ★ ➔ ★ . ∀ fm : RecFunctor · F. F · (D · F fm) ➔ D · F fm =
  Λ F . Λ fm . λ d .
    dFold · F -fm [ in' · F -fm
                     (cast · (F · (D · F fm)) · (F · (C · F))
                        -(fm · (D · F fm) · (C · F) [λ x . (dUnfold · F -fm x).1 , β]) d) ,
                    Λ P . λ palg . palg · (D · F fm) [ out · F -fm , confirmOut] -[λ x . (dUnfold · F -fm x).1 , β] -[λ x . x , β]
                                    -(cast · (F · (D · F fm)) · (F · (C · F))
                                       -(fm · (D · F fm) · (C · F) [λ x . (dUnfold · F -fm x).1 , β]) d)
                                       [ λ r . (dUnfold · F -fm r).2 · P palg , β{foldU palg}] [[d,d],β{d}]].

confirmIn ◂ inU ≃ in = β{in}.

inAlg ◂ ∀ F : ★ ➔ ★ . ∀ fm : RecFunctor · F. Alg · F · (C · F) · (D · F fm) =
  Λ F . Λ fm . 
    [ β{λ pred . λ eval . λ p . (in p)} ,
      Λ R . Λ reveal . λ pred . λ eval . λ p .
        in · F -fm (cast · (F · R) · (F · (D · F fm))
                     -(fm · R · (D · F fm)
                         (caste · R · (D · F fm) eval.1
                          -(λ r . ρ eval.2 - ρ (rUnfold · F -fm (cast · R · (C · F) -reveal r)).2 - β))) p.1) ] .

confirmInAlg ◂ inAlg ≃ inAlgU = β{inAlg}.

