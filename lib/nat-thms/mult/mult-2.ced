import nat-thms/mult/mult-0.
import nat-thms/add/add-1.

mult-S-1 ⇐ Π x : Nat . Π y : Nat . mult x ( S y ) ≃ add x ( mult x y ) =
  λ x . λ y . θ x
    ( λ px . λ hx .
      ρ ( mult-S-0 px ( S y ) ) -
      ρ hx -
      ρ ( mult-S-0 px y ) -
      ρ ( add-S-comm-0 y ( add px ( mult px y ) ) ) -
      ρ ( add-S-comm-0 px ( add y ( mult px y ) ) ) -
      ρ ( add-assoc y px ( mult px y ) ) -
      ρ ( add-assoc px y ( mult px y ) ) -
      ρ ( add-comm y px ) -
      β )
    β .

mult-S ⇐ Π x : Nat . Π y : Nat . mult ( S x ) ( S y ) ≃ S ( add ( add x y ) ( mult x y ) ) =
  λ x . λ y .
    ρ ( mult-S-1 ( S x ) y ) -
    ρ ( mult-S-0 x y ) -
    ρ ( add-assoc ( S x ) y ( mult x y ) ) -
    ρ ( add-S-comm-0 x y ) -
    ρ ( add-S-comm-0 ( add x y ) ( mult x y ) ) -
    β.

mult-one-0 ⇐ Π x : Nat . mult ( S Z ) x ≃ x =
  λ x . θ x
    ( λ px . λ hx . ρ ( mult-S Z px ) - ρ ( add-Z-1 px ) - β )
    β .

mult-comm ⇐ Π x : Nat . Π y : Nat .  mult x y ≃ mult y x =
  λ x . θ x
    ( λ px . λ hx . λ y . θ y
      ( λ py . λ hy .
        ρ ( mult-S px py ) -
	ρ ( mult-S py px ) -
	ρ ( add-comm py px ) -
	ρ ( hx py ) -
	β )
      ρ ( mult-Z-1 ( S px ) ) - β )
    ( λ y . θ y
      ( λ py . λ _ . ρ ( mult-Z-1 ( S py ) ) - β ) 
      β ) .

mult-distr-lemma-0 ⇐ Π a : Nat . Π b : Nat . Π c : Nat . Π d : Nat . add ( add a b ) ( add c d ) ≃ add ( add a c ) ( add b d ) =
  λ a . λ b . λ c . λ d .
    ρ ( add-assoc ( add a b ) c d ) -
    ρ ( add-comm a b ) -
    ρ ς ( add-assoc b a c ) -
    ρ ( add-comm b ( add a c ) ) -
    ρ ς ( add-assoc ( add a c ) b d ) -
    β .

mult-distr-lemma-1 ⇐ Π a : Nat . Π b : Nat . Π c : Nat . Π d : Nat . add ( add a ( add b c ) ) ( add a d ) ≃ add ( add ( add a b ) ( add a c ) ) d  =
  λ a . λ b . λ c . λ d .
    ρ ( add-assoc ( add a ( add b c ) ) a d ) -
    ρ ( add-comm a ( add b c ) ) -
    ρ ς ( add-assoc ( add b c ) a a ) -
    ρ ( add-comm ( add b c ) ( add a a ) ) -
    ρ ( mult-distr-lemma-0 a a b c ) -
    β .

mult-distr ⇐ Π x : Nat . Π y : Nat . Π z : Nat . mult x ( add y z ) ≃ add ( mult x y ) ( mult x z ) =
  λ x . θ x
    ( λ px . λ hx . λ y . θ y
      ( λ py . λ hy . λ z . θ z
        ( λ pz . λ hz .
	  ρ ( add-S-comm py pz ) -
	  ρ ( mult-S px ( S ( add py pz ) ) ) -
	  ρ ( mult-S px py ) -
	  ρ ( mult-S px pz ) -
	  ρ ( add-S-comm-1 px ( add py pz ) ) -
	  ρ ( add-S-comm-0 ( add px ( add py pz ) ) ( mult px ( S ( add py pz ) ) ) ) -
	  ρ ( mult-S-1 px ( add py pz ) ) -
	  ρ ( mult-distr-lemma-1 px py pz ( mult px ( add py pz ) ) ) -
	  ρ ( add-S-comm ( add ( add px py ) ( mult px py ) ) ( add ( add px pz ) ( mult px pz ) ) ) -
	  ρ ( mult-distr-lemma-0 ( add px py ) ( mult px py ) ( add px pz ) ( mult px pz ) ) -
	  ρ ς ( hx py pz ) -
	  β )
	( ρ ( add-Z-1 ( S py ) ) -
	  ρ ( mult-Z-1 ( S px ) ) -
	  ρ ( add-Z-1 ( mult ( S px ) ( S py ) ) ) -
	  β ) )
      ( λ pz .
        ρ ( mult-Z-1 ( S px ) ) -
	β ) )
    ( λ _ . λ _ . β ) .

mult-assoc-lemma-0 ⇐ Π a : Nat . Π b : Nat . Π c : Nat . Π d : Nat . Π e : Nat . add ( add a b ) ( add ( add c d ) e ) ≃ add ( add a c ) ( add ( add b d ) e ) =
  λ a . λ b . λ c . λ d . λ e .
    ρ ( add-assoc ( add a b ) ( add c d ) e ) -
    ρ ( mult-distr-lemma-0 a b c d ) -
    ρ ς ( add-assoc ( add a c ) ( add b d ) e ) -
    β .

mult-assoc-lemma-1 ⇐ Π x : Nat . Π y : Nat . Π z : Nat . add x ( add y z ) ≃ add y ( add z x ) =
  λ x . λ y . λ z .
    ρ ( add-comm y z ) -
    ρ ( add-assoc x z y ) -
    ρ ( add-comm ( add x z ) y ) -
    ρ ( add-comm x z ) -
    β .

mult-assoc ⇐ Π x : Nat . Π y : Nat . Π z : Nat . mult x ( mult y z ) ≃ mult ( mult x y ) z =
  λ x . θ x
    ( λ px . λ hx . λ y . θ y
      ( λ py . λ hy . λ z . θ z
        ( λ pz . λ hz .
	  ρ ( mult-S py pz ) -
 	  ρ ( mult-S px ( add ( add py pz ) ( mult py pz ) ) ) -
	  ρ ( mult-S px py ) -
	  ρ ( mult-S ( add ( add px py ) ( mult px py ) ) pz ) -
	  ρ ( mult-distr px ( add py pz ) ( mult py pz ) ) -
	  ρ ( mult-distr px py pz ) -
	  ρ ( add-assoc px ( add py pz ) ( mult py pz ) ) -
	  ρ ( mult-assoc-lemma-0 ( add px ( add py pz ) ) ( mult py pz ) ( mult px py ) ( mult px pz ) ( mult px ( mult py pz ) ) ) -
	  ρ ( add-comm ( mult py pz ) ( mult px pz ) ) -
	  ρ ( mult-comm ( add ( add px py ) ( mult px py ) ) pz ) -
	  ρ ( mult-distr pz ( add px py ) ( mult px py ) ) -
	  ρ ( mult-distr pz px py ) -
	  ρ ( add-comm ( add ( add px py ) ( mult px py ) ) pz ) -
	  ρ ( add-assoc pz ( add px py ) ( mult px py ) ) -
	  ρ ( mult-assoc-lemma-1 pz px py ) -
	  ρ ( mult-comm pz ( mult px py ) ) -
	  ρ ς ( hx py pz ) -
	  ρ ( mult-comm pz px ) -
	  ρ ( mult-comm pz py ) -
	  β )
	( ρ ( mult-Z-1 ( S py ) ) -
	  ρ ( mult-Z-1 ( S px ) ) -
	  ρ ( mult-Z-1 ( mult ( S px ) ( S py ) ) ) -
	  β ) )
      ( λ z . ρ ( mult-Z-1 ( S px ) ) - β ) )
    ( λ y . λ z . β ) .
