import bool.

rec Nat | S : Nat → Nat , Z : Nat =
  ∀ P : Nat → ★ .
    (Π n : Nat . P n → P (S n)) → P Z → P self
  with
    S = λ n . Λ P . λ s . λ z . s n (n · P s z) ,
    Z = Λ P . λ s . λ z . z.

add ⇐ Nat → Nat → Nat = λ n . λ m . θ n (λ n . S) m .

CNat ⇐ ★ = ∀ X : ★ . (X → X) → X → X .

toCNat ⇐ Nat → CNat =
  λ x . x · (λ x : Nat . CNat)
          (λ p . λ c . (Λ X . λ s . λ z . s (c · X s z)))
          (Λ X . λ s . λ z . z) .

iszero ⇐ Nat → Bool = 
  λ n . n · (λ n : Nat . Bool) (λ p . λ r . ff) tt .

eqnat ⇐ Nat → Nat → Bool =
  λ n . n · (λ n : Nat . Nat → Bool) 
          (λ p . λ r . λ m . m · (λ m : Nat . Bool) (λ p' . λ u . r p') ff)
          iszero .


