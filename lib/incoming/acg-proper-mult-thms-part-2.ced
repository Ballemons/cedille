import acg-proper-mult-thms-part-1.

add-S-comm ⇐ Π x : Nat . Π y : Nat . add ( S x ) ( S y ) ≃ S ( S ( add x y ) ) =
  λ x . λ y . ρ ( add-S-comm-0 x (S y) ) - ρ ( add-S-comm-1 x y ) - β .

mult-distr-lemma-0 ⇐ Π a : Nat . Π b : Nat . Π c : Nat . Π d : Nat . add ( add a b ) ( add c d ) ≃ add ( add a c ) ( add b d ) =
  λ a . λ b . λ c . λ d .
    ρ ( add-assoc ( add a b ) c d ) -
    ρ ( add-comm a b ) -
    ρ ς ( add-assoc b a c ) -
    ρ ( add-comm b ( add a c ) ) -
    ρ ς ( add-assoc ( add a c ) b d ) -
    β .

mult-distr-lemma-1 ⇐ Π a : Nat . Π b : Nat . Π c : Nat . Π d : Nat . add ( add a ( add b c ) ) ( add a d ) ≃ add ( add ( add a b ) ( add a c ) ) d  =
  λ a . λ b . λ c . λ d .
    ρ ( add-assoc ( add a ( add b c ) ) a d ) -
    ρ ( add-comm a ( add b c ) ) -
    ρ ς ( add-assoc ( add b c ) a a ) -
    ρ ( add-comm ( add b c ) ( add a a ) ) -
    ρ ( mult-distr-lemma-0 a a b c ) -
    β .

mult-distr ⇐ Π x : Nat . Π y : Nat . Π z : Nat . mult x ( add y z ) ≃ add ( mult x y ) ( mult x z ) =
  λ x . θ x
    ( λ px . λ hx . λ y . θ y
      ( λ py . λ hy . λ z . θ z
        ( λ pz . λ hz .
	  ρ ( add-S-comm py pz ) -
	  ρ ( mult-S px ( S ( add py pz ) ) ) -
	  ρ ( mult-S px py ) -
	  ρ ( mult-S px pz ) -
	  ρ ( add-S-comm-1 px ( add py pz ) ) -
	  ρ ( add-S-comm-0 ( add px ( add py pz ) ) ( mult px ( S ( add py pz ) ) ) ) -
	  ρ ( mult-S-1 px ( add py pz ) ) -
	  ρ ( mult-distr-lemma-1 px py pz ( mult px ( add py pz ) ) ) -
	  ρ ( add-S-comm ( add ( add px py ) ( mult px py ) ) ( add ( add px pz ) ( mult px pz ) ) ) -
	  ρ ( mult-distr-lemma-0 ( add px py ) ( mult px py ) ( add px pz ) ( mult px pz ) ) -
	  ρ ς ( hx py pz ) -
	  β )
	( ρ ( add-Z-1 ( S py ) ) -
	  ρ ( add-Z-1 ( mult (S px) (S py) ) ) -
	  ρ ( mult-Z-1 ( S px ) ) -
	  ρ ( add-Z-1 ( mult ( S px ) ( S py ) ) ) -
	  β ) )
      ( λ pz .
        ρ ( mult-Z-1 ( S px ) ) -
	β ) )
    ( λ _ . λ _ . β ) .
