import acg-proper-mult-thms-part-3.

exp ⇐ Nat → Nat → Nat =
  λ x . λ y . θ y
    ( λ _ . λ hy . mult x hy )
    ( S Z ) .

exp-defn ⇐ Π b : Nat . Π e : Nat . exp b ( S e ) ≃ mult b ( exp b e ) =
  λ b . λ e . β .

exp-one-0 ⇐ Π e : Nat . exp ( S Z ) e ≃ ( S Z ) =
  λ e . θ e
    ( λ pe . λ he .
      ρ ( exp-defn ( S Z ) pe ) -
      ρ he -
      ρ ( mult-one-1 ( S Z ) ) -
      β )
    β .

exp-one-1 ⇐ Π b : Nat . exp b ( S Z ) ≃ b =
  λ b . ρ ( exp-defn b ( S Z ) ) - ρ ( mult-one-1 b ) - β .

exp-add-distr ⇐ Π x : Nat . Π a : Nat . Π b : Nat . exp x ( add a b ) ≃ mult ( exp x a ) ( exp x b ) =
  λ x . λ a . θ a
    ( λ pa . λ ha . λ b . θ b
      ( λ pb . λ _ .
        ρ ( add-S-comm pa pb ) -
	ρ ( exp-defn x ( S ( add pa pb ) ) ) -
	ρ ( exp-defn x ( add pa pb ) ) -
	ρ ( ha pb ) -
	ρ ( mult-assoc x ( exp x pa ) ( exp x pb ) ) -
	ρ ( mult-comm ( mult x ( exp x pa ) ) ( exp x pb ) ) -
	ρ ( mult-assoc x ( exp x pb ) ( mult x ( exp x pa ) ) ) -
	ρ ( mult-comm ( mult x ( exp x pb ) ) ( mult x ( exp x pa ) ) ) -
	β )
      ( ρ ( add-Z-1 (S pa) ) - ρ ( mult-one-1 ( exp x (S pa) ) ) - β ) ) 
    ( λ b . ρ ( add-Z-0 b ) - ρ ( mult-one-0 ( exp x b ) ) - β ) .

exp-mult-distr-0 ⇐ Π x : Nat . Π y : Nat . Π a : Nat . exp ( mult x y ) a ≃ mult ( exp x a ) ( exp y a ) =
  λ x . λ y . λ a . θ a
    ( λ pa . λ ha .
      ρ ( exp-defn ( mult x y ) pa ) -
      ρ ( exp-defn x pa ) -
      ρ ( exp-defn y pa ) -
      ρ ha -
      ρ ς ( mult-assoc x y ( mult ( exp x pa ) ( exp y pa ) ) ) -
      ρ ( mult-comm ( exp x pa ) ( exp y pa ) ) -
      ρ ( mult-assoc y ( exp y pa ) ( exp x pa ) ) -
      ρ ( mult-comm ( mult y ( exp y pa ) ) ( exp x pa ) ) -
      ρ ( mult-assoc x ( exp x pa ) ( mult y ( exp y pa ) ) ) -
      β )
    β .

exp-mult-distr-1 ⇐ Π x : Nat . Π a : Nat . Π b : Nat . exp x ( mult a b ) ≃ exp ( exp x a ) b =
  λ x . λ a . θ a
    ( λ pa . λ ha . λ b . θ b
      ( λ pb . λ hb .
        ρ ( mult-S pa pb ) -
	ρ ( exp-defn x ( add ( add pa pb ) ( mult pa pb ) ) ) -
	ρ ( exp-add-distr x ( add pa pb ) ( mult pa pb ) ) -
	ρ ( ha pb ) -
	ρ ( exp-add-distr x pa pb ) -
	ρ ( exp-defn x pa ) -
	ρ ( exp-defn ( mult x ( exp x pa ) ) pb ) -
	ρ ( exp-mult-distr-0 x ( exp x pa ) pb ) -
	ρ ( mult-assoc x ( mult ( exp x pa ) ( exp x pb ) ) ( exp ( exp x pa ) pb ) ) -
	ρ ( mult-assoc x ( exp x pa ) ( exp x pb ) ) -
	ρ ς ( mult-assoc ( mult x ( exp x pa ) ) ( exp x pb ) ( exp ( exp x pa ) pb ) ) -
	β )
      ( ρ ( mult-Z-1 ( S pa ) ) -
        β ) )
    ( λ b . ρ ( mult-Z-0 b  ) - ρ ( exp-one-0 b ) - β ) .
