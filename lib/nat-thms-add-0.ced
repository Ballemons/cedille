import nat.
import false.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%						%
%  add-defn : Addition is iterated succession	%
%						%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  add-defn ⇐ Π x : Nat . Π y : Nat .

  % Theorem

  add x y ≃ x ( λ p . S ) y =

  % Proof

  λ x . λ y . β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%						%
%  add-Z : Z is the identity for addition	%
%						%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  add-Z-0 ⇐ Π n : Nat .

  % Theorem

  add Z n ≃ n =

  % Proof

  λ n . β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  add-Z-1 ⇐ Π n : Nat .

  % Theorem

  add n Z ≃ n =
  
  % Inductive Step

  λ n . θ n ( λ p . λ h . εl ρ h
      	      	      	       - β )

  % Base Case
  
  β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%						%
%  add-S-comm : Addition commutes with the	%
%    successor function				%
%						%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  add-S-comm-0 ⇐ Π n : Nat . Π m : Nat .

  % Theorem

  add ( S n ) m ≃ S ( add n m ) =

  % Proof

  λ n . λ m . β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  add-S-comm-1 ⇐ Π n : Nat . Π m : Nat .

  % Theorem

  add n ( S m ) ≃ S ( add n m ) =

  % Inductive Step

  λ n . λ m . θ n ( λ p . λ h . εl ρ h
      	      	      	      	   - β )

  % Base Case

  β .

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%						%
%  add-assoc : Addition is associative		%
%						%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  add-assoc ⇐ Π x : Nat . Π y : Nat . Π z : Nat .

  % Theorem

  add x ( add y z ) ≃ add ( add x y ) z =

  % Inductive Step

  λ x . λ y . λ z . θ x ( λ p . λ h . εl ρ h - β )

  % Base Case

  β .
