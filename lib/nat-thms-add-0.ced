import nat.
import false.

add-defn ⇐ Π x : Nat . Π y : Nat . add x y ≃ x ( λ p . S ) y =
  λ x . λ y . β .

add-Z-0 ⇐ Π n : Nat . add Z n ≃ n =
  λ n . β .

add-Z-1 ⇐ Π n : Nat . add n Z ≃ n =
  λ n . θ n ( λ p . λ h . εl ρ h
      	      	      	       - β )
  β .

add-S-comm-0 ⇐ Π n : Nat . Π m : Nat . add ( S n ) m ≃ S ( add n m ) =
  λ n . λ m . β .

add-S-comm-1 ⇐ Π n : Nat . Π m : Nat . add n ( S m ) ≃ S ( add n m ) =
  λ n . λ m . θ n ( λ p . λ h . εl ρ h
      	      	      	      	   - β )
  β .

add-assoc ⇐ Π x : Nat . Π y : Nat . Π z : Nat . add x ( add y z ) ≃ add ( add x y ) z =
  λ x . λ y . λ z . θ x ( λ p . λ h . εl ρ h - β )
  β .

add-S-hop ⇐ Π a : Nat . Π b : Nat . add a (S b) ≃ add (S a) b =
  λ a . λ b .
   ρ (add-S-comm-1 a b) - β .
