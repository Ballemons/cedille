Bool ◂ ★ = ∀ X : ★ . X ➔ X ➔ X .
tt ◂ Bool = Λ X . λ cT . λ cF . cT .
ff ◂ Bool = Λ X . λ cT . λ cF . cF .

If ◂ ★ ➔ ★ ➔ Bool ➔ ★ =  λ A : ★ . λ B : ★ . λ b : Bool .
  ↑ X . b · X : (☆ ➔ ☆ ➔ ☆) · A · B .

Eq ◂ Π A : ★ . A ➔ A ➔ ★
  = λ A : ★ . λ x : A . λ y : A .
  x ≃ y .

subst ◂ ∀ A : ★ . ∀ P : A ➔ ★ .
  ∀ x : A . ∀ y : A .
  Eq · A y x ➾ P x ➔ P y
  = Λ A . Λ P . Λ x . Λ y . Λ q . λ p .
  ρ q - p .

Clash ◂ ★ = Eq · Bool ff tt .

cast ◂ ∀ A : ★ . ∀ B : ★ . Clash ➾ A ➔ B
  = Λ A . Λ B . Λ q . λ a .
  subst · Bool · (If · A · B) -tt -ff -q a .

self ◂ ∀ X : ★ . Clash ➾ X ➔ X
  = Λ X . Λ q . λ x . (cast · X · (X ➔ X) -q x) x .

omega ◂ ∀ X : ★ . Clash ➾ X
  = Λ X . Λ q . (self · X -q) (cast · (X ➔ X) · X -q (self · X -q)) .

loop ◂ ∀ X : ★ . ∀ q : Clash .
  ∀ x : X . x ≃ omega · X -q
  = Λ X . Λ q . Λ x . β .

