module core/rec (F: ★ ➔ ★).

import top.
import cast.

Rec : (★ ➔ ★) ➔ ★
= λ F: ★ ➔ ★. ∀ X: ★. Cast ·(F ·X) ·X ➾ X.

recCast : ∀ F: ★ ➔ ★. ∀ X: ★. Cast ·(F ·X) ·X ➾ Cast ·(Rec ·F) ·X
= Λ F. Λ X. Λ c. [λ d. d -c , β].

recIn : CastMap ·F ➾ Cast ·(F ·(Rec ·F)) ·(Rec ·F)
= Λ im.
  [f : F ·(Rec ·F) ➔ Rec ·F
    = λ fd. Λ X. Λ c.
      elimCast -c (castMap -im -(recCast -c) fd)]
- [ f , β].

recFold = Λ im: CastMap ·F. elimCast -(recIn -im).

recOut : CastMap ·F ➾ Cast ·(Rec ·F) ·(F ·(Rec ·F))
= Λ im.
  [f : Rec ·F ➔ F ·(Rec ·F)
    = λ d. d ·(F ·(Rec ·F)) -(im (recIn -im))]
- [ f , β].

recUnfold = Λ im: CastMap ·F. elimCast -(recOut -im).

_ : {recFold ≃ λ x. x} = β.
_ : {recUnfold ≃ λ x. x} = β.
