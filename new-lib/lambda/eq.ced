module eq.

import top.
import bot.

kleene : Π a: Top. Π b: Top. {a ≃ b} ➔ Top ➔ {a ≃ b}
  = λ a. λ b. λ eq. λ k. ρ eq - β{k}.

phi : ∀ A: ★. Π b: Top. ∀ a: A. {a ≃ b} ➾ A
  = Λ A. λ b. Λ a. Λ eq. φ eq - a {b}.

delta : {λ x. λ y. x ≃ λ x. λ y. y} ➔ False
  = λ eq. δ - eq.

rho : ∀ P: Top ➔ ★. ∀ t1: Top. ∀ t2: Top. P t1 ➔ {t2 ≃ t1} ➾ P t2
  = Λ P. Λ t1. Λ t2. λ p. Λ eq. ρ eq - p.

subst = rho.

trans : Π x: Top. Π y: Top. Π z: Top. {x ≃ y} ➔ {y ≃ z} ➔ {x ≃ z}
  = λ x. λ y. λ z. λ eq1. λ eq2. ρ eq1 - eq2.

cong : Π f: Top. Π x: Top. Π y: Top. {x ≃ y} ➔ {f x ≃ f y}
  = λ f. λ x. λ y. λ eq. ρ eq - β.
